$title Princeton Bilevel Optimization Example 9.1.2 (FLDS912,SEQ=28)

$ontext

  Test problem 9.2.3 in Handbook of Test Problems in Local and Global Optimization
  Test problem 9.1.2 on http://titan.princeton.edu/TestProblems/chapter9.html

References:

Floudas, C A, Pardalos, P M, Adjiman, C S, Esposito, W R, Gumus, Z H, Harding,
S T, Klepeis, J L, Meyer, C A, and Schweiger, C A, Handbook of Test Problems in
Local and Global Optimization. Kluwer Academic Publishers, 1999.

Liu, Y H, and Hart, S M, Characterizing an Optimal Solution to the Linear Bilevel
Programming Problem. European Journal of Operational Research 79 (1994), 164-166.

Contributor: Alex Meeraus and Jan-H. Jagla, December 2009

$offtext

*Solution of problem 9.1.2 on the web
scalar x_l /    4 /
       y_l /    4 /
       tol / 1e-6 /;

variables z, z_in; positive variables x, y;
equation  ob, c2, c3, c4, c5;

ob..  -   x - 3*y =e=  z;

c2..  -   x +   y =l=  3;
c3..      x + 2*y =l= 12;
c4..    4*x -   y =l= 12;
c5..        -   y =l=  0;

model bilevel / all /;

$echo bilevel x min y c2 c3 c4 c5 > "%emp.info%"

*Start from reported solution
x.l = x_l;
y.l = y_l;

solve bilevel using EMP minimizing z;

abort$(   (abs(x.l - x_l) > tol)
       or (abs(y.l - y_l) > tol) ) 'Deviated from reported solution';
