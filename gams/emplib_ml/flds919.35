$title Princeton Bilevel Optimization Example 9.1.9 (FLDS919,SEQ=35)

$ontext

  Test problem 9.2.10 in Handbook of Test Problems in Local and Global Optimization
  Test problem 9.1.9 on http://titan.princeton.edu/TestProblems/chapter9.html

References:

Floudas, C A, Pardalos, P M, Adjiman, C S, Esposito, W R, Gumus, Z H, Harding,
S T, Klepeis, J L, Meyer, C A, and Schweiger, C A, Handbook of Test Problems in
Local and Global Optimization. Kluwer Academic Publishers, 1999.

Visweswaran, V., C. Floudas, M. Ierapetritou, and E. Pistikopoulos, A
Decomposition Based Global Optimization Approach for Solving Bilevel Linear and
Nonlinear Quadratic Programs. In Floudas and Pardalos (eds.), State of the Art
in Global Optimization: Computational Methods and Applications. Kluwer Academic
Publishers, 1996.

Contributor: Alex Meeraus and Jan-H. Jagla, December 2009

$offtext

*Solution of problem 9.1.9 on the web
scalar  x_l,
        y_l,
        tol / 1e-6 /;
x_l = 8/9;
y_l = 20/9;

variables z; positive variable x, y;
equations ob, c2, c3, c4, c5, c6;

ob..        x +     y =e= z;

c2..       -x - 0.5*y =l= -2;
c3..  -0.25*x +     y =l=  2;
c4..        x + 0.5*y =l=  8;
c5..        x -   2*y =l=  2;
c6..          -     y =l=  0;

model bilevel / all /;

$echo bilevel x max y c2 c3 c4 c5 c6 > "%emp.info%"

*Start from reported solution
x.l = x_l;
y.l = y_l;

solve bilevel using EMP minimizing z;

abort$(   (abs(x.l - x_l) > tol)
       or (abs(y.l - y_l) > tol) ) 'Deviated from reported solution';

