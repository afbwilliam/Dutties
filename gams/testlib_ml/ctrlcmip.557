$title Test interrupt handling (CTRLCMIP,SEQ=557)

$onecho > mip.gms

SETS s scenarios / 1*5000/
     t time periods / t1*t12 /
     j components / C001*C010 /

alias(t,tt);

PARAMETERS
     di(s,t) increment
     D(t,s)  demand
     c(j)    capacity size
     p(j)    capacity cost
     mu      mean capacity parameter
     sigma   std capacity parameter;

mu = 100; sigma = 10;

c(j) = round(uniform(1,mu));
p(j) = round(mu + c(j) + uniform(-sigma,sigma));

di(s,t)$(ord(s) <= 0.25*card(s)) = round(normal( 50,10));
di(s,t)$(ord(s)  > 0.25*card(s) and ord(s) <= 0.75*card(s)) = round(normal(100,20));
di(s,t)$(ord(s)  > 0.75*card(s)) = round(normal(150,40));

d(t,s) = sum(tt$(ord(tt) <= ord(t)), di(s,tt));
*display c,p,di,d;

parameters dis(t) discount factor
           w      shortage penalty;

dis(t) = power(.86,ord(t)-1);
w      = 5;

variables x(j,t) expansion
          z(s) max capacity shortage
          cap(t) installed capacity
          obj;

integer variable x; positive variable z;

equations capbal(t)   capacity balance
          dembal(t,s) demand balance
          objdef;


objdef.. obj =e= sum((j,t), dis(t)*p(j)*x(j,t))
               + w/card(s)*sum(s, z(s));

capbal(t).. cap(t) =e= cap(t-1) + sum(j, c(j)*x(j,t));

dembal(t,s).. cap(t) + z(s) =g= d(t,s);

model rotdk / all /;

option optcr=0.05,limcol=0,limrow=0;

solve rotdk min obj us mip;

file fx 'test'; put fx
put_utility 'log' / 'Solver Status = ' rotdk.solvestat
put_utility 'log' / 'Model Status  = ' rotdk.modelstat;

$offecho

$onechoV > miptest.inc
$call =ask M="Please click on Interrupt during Branch&Bound"

$call gams mip.gms lo=3 mip=%1 sl=%2

$call =ask T=integer M="Solver Status from mip.lst" L=1 U=13 o=ss.inc
scalar %1%2ss 'solver status' /
$include ss.inc
/;

if(%1%2ss <> 8,
   put 'Solver status should be 8 (User Interrupt) but is ' %1%2ss;
   err = err + 1;
);

$ontext
$call =ask T=integer M="Model Status mip.lst" L=1 U=19 o=ms.inc
scalar %1%2ms 'model status' /
$include ms.inc
/;

$offtext
$offecho

scalar err /0/;
file f /'ctrlcfails.txt'/; put f;
$batinclude miptest.inc baron       0
$batinclude miptest.inc cplex       0
$batinclude miptest.inc cplex       5
$batinclude miptest.inc gurobi      0
$batinclude miptest.inc gurobi      5
$batinclude miptest.inc lindoglobal 0
$batinclude miptest.inc lindoglobal 5
$batinclude miptest.inc mosek       0
$batinclude miptest.inc mosek       5
$batinclude miptest.inc osl         0
$batinclude miptest.inc scip        0
$batinclude miptest.inc scip        5
$batinclude miptest.inc xpress      0
$batinclude miptest.inc xpress      5
putclose;

execute$(err > 0) 'cat ctrlcfails.txt';
abort$(err > 0)   'Some of the runs failed';
