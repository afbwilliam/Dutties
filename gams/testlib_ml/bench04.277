$TITLE 'BENCH test suite - basic option reading test' (BENCH04,SEQ=277)

$ontext
The first solve should return modelstat 14 (no sol returned)
The bench option 'returnlastsol on' should give us a solution.
$offtext

Sets
     i   canning plants   / seattle, san-diego /
     j   markets          / new-york, chicago, topeka / ;

Parameters

     a(i)  capacity of plant i in cases
       /    seattle     350
            san-diego   600  /

     b(j)  demand at market j in cases
       /    new-york    325
            chicago     300
            topeka      275  / ;

Table d(i,j)  distance in thousands of miles
                  new-york       chicago      topeka
    seattle          2.5           1.7          1.8
    san-diego        2.5           1.8          1.4  ;

Scalar f  freight in dollars per case per thousand miles  /90/ ;

Parameter c(i,j)  transport cost in thousands of dollars per case ;

          c(i,j) = f * d(i,j) / 1000 ;

Variables
     x(i,j)  shipment quantities in cases
     z       total transportation costs in thousands of dollars ;

Positive Variable x ;

Equations
     cost        define objective function
     supply(i)   observe supply limit at plant i
     demand(j)   satisfy demand at market j ;

cost ..        z  =e=  sum((i,j), c(i,j)*x(i,j)) ;

supply(i) ..   sum(j, x(i,j))  =l=  a(i) ;

demand(j) ..   sum(i, x(i,j))  =g=  b(j) ;

Model transport /all/ ;

$onecho > bench.op2
solvers bdmlp
$offecho

$onecho > bench.525
solvers bdmlp
returnlastsol on
$offecho

option lp = bench;

transport.optfile = 2;
Solve transport using lp minimizing z ;
abort$(transport.modelstat <> %modelstat.NoSolutionReturned% or transport.solvestat <> %solvestat.NormalCompletion%) 'problems with bench option';

transport.optfile = 525;
Solve transport using lp minimizing z ;
abort$(transport.modelstat <> %modelstat.Optimal% or transport.solvestat <> %solvestat.NormalCompletion%) 'problems with bench option';
abort$(abs(transport.objval-153.675) > 1e-5) 'Bad obj for transport';
