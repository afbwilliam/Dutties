$ontext

Report writing part of transport model
illustrate include procedure along with tranint.gms
also used to illustrate save restart

$offtext

PARAMETER MOVEMENT(*,*)  COMMODITY MOVEMENT;
MOVEMENT(PLANT,MARKET)=SHIPMENTS.L(PLANT,MARKET);
MOVEMENT("TOTAL",MARKET)=SUM(PLANT,SHIPMENTS.L(PLANT,MARKET));
MOVEMENT(PLANT,"TOTAL")=SUM(MARKET,SHIPMENTS.L(PLANT,MARKET));
MOVEMENT("TOTAL","TOTAL")=SUM(MARKET,MOVEMENT("TOTAL",MARKET));
OPTION DECIMALS=0;
DISPLAY MOVEMENT;
PARAMETER COSTS(*,*)  COMMODITY MOVEMENT COSTS BY ROUTE;
COSTS(PLANT,MARKET)=COST(PLANT,MARKET)*SHIPMENTS.L(PLANT,MARKET);
COSTS("TOTAL",MARKET)
    =SUM(PLANT,COST(PLANT,MARKET)*SHIPMENTS.L(PLANT,MARKET));
COSTS(PLANT,"TOTAL")
    =SUM(MARKET,COST(PLANT,MARKET)*SHIPMENTS.L(PLANT,MARKET));
COSTS("TOTAL","TOTAL")=TCOST.L;
OPTION DECIMALS=0;
*file to illustrate include and nested include along with tranint.gms
DISPLAY COSTS;
PARAMETER SUPPLYREP(PLANT,*)  SUPPLY REPORT;
SUPPLYREP(PLANT,"AVAILABLE")=SUPPLY(PLANT);
SUPPLYREP(PLANT,"USED")=MOVEMENT(PLANT,"TOTAL");
SUPPLYREP(PLANT,"MARGVALUE")=ABS(SUPPLYEQ.M(PLANT));
OPTION DECIMALS=2;
DISPLAY SUPPLYREP;
PARAMETER DEMANDREP(MARKET,*)  DEMAND REPORT;
DEMANDREP(MARKET,"REQUIRED")=DEMAND(MARKET);
DEMANDREP(MARKET,"RECIEVED")=MOVEMENT("TOTAL",MARKET);
DEMANDREP(MARKET,"MARGCOST")=ABS(DEMANDEQ.M(MARKET));
$include trannest
OPTION DECIMALS=2;
DISPLAY DEMANDREP;
PARAMETER CMOVEMENT(*,*) COSTS OF CHANGING COMMODITY MOVEMENT PATTERN;
CMOVEMENT(PLANT,MARKET)=SHIPMENTS.M(PLANT,MARKET);
OPTION DECIMALS=2;
DISPLAY CMOVEMENT;

$ontext
#user model library stuff
Main topic Report writing
Featured item 1 .L attribute
Featured item 2 .M attribute
Featured item 3  Include
Featured item 4  Save restart
include (tranint.gms,trandata.gms,tranmodl.gms)
Description
Report writing part of transport model
illustrate include procedure along with tranint.gms
also used to illustrate save restart

$offtext
