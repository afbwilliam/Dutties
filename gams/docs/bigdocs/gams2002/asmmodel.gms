$ontext

Model structure of ASM model

$offtext

*$setglobal solveasm "nonlinear";
$setglobal solveasm 'linear';

*#######################################################################
* THIS MODEL HAS ADDED NEW ACTIVITIES FOR NON-AG SOURCES OF SUPPLY
*     FOR BOTH PRIMARY AND SECONDARY COMMODITIES
*#######################################################################

*#######################################################################
* DEMAND CURVES SPECIFICATIONS IN ASM INCLUDE THE FOLLOWING 4 CASES:
*(1) NO FINAL DEMAND:   P NE 0.0;  Q EQ 0.0;  ELAS EQ 0.0
*(2) REGULAR DEMAND:    P NE 0.0;  Q NE 0.0;  ELAS LE -0.05
*(3) HORIZONATAL DEMAND:P NE 0.0;  Q NE 0.0;  ELAS EQ 0.0 (FIXED PRICE DEMAND)
*(4) VERTICAL DEMAND:   P NE 0.0;  Q NE 0.0;  W/ MINQ GT 0.0
* THESE SPEICIFICATIONS ALSO APPLY TO THE IMPORT ANS INPUT SUPPLY CURVES.
*#######################################################################

POSITIVE VARIABLES

DEMANDP(PRIMARY)                       PRIMARY PRODUCT DOMESTIC DEMAND
DEMANDPS(PRIMARY,STEPS)                PRIMARY DOMESTIC DEMAND BY STEP
IMPORTP(PRIMARY)                       PRIMARY PRODUCT IMPORTS
IMPORTPS(PRIMARY,STEPS)                PRIMARY PRODUCT IMPORTS BY STEP
EXPORTP(PRIMARY)                       PRIMARY PRODUCT EXPORTS
EXPORTPS(PRIMARY,STEPS)                PRIMARY PRODUCT EXPORTS BY STEP
DEMANDS(SECONDARY)                     SECONDARY PRODUCT DOMESTIC DEMAND
DEMANDSS(SECONDARY,STEPS)              SECONDARYDOMESTIC DEMAND BY STEP
IMPORTS(SECONDARY)                     SECONDARY PRODUCT IMPORTS
IMPORTSS(SECONDARY,STEPS)              SECONDARY PRODUCT IMPORTS BY STEP
EXPORTS(SECONDARY)                     SECONDARY PRODUCT EXPORTS
EXPORTSS(SECONDARY,STEPS)              SECONDARY PRODUCT EXPORTS BY STEP
LANDSUPPLY(SUBREG,LANDTYPE)            REGIONAL LAND SUPPLY
LANDSUPPLS(SUBREG,LANDTYPE,STEPS)      LAND SUPPLY BY STEP
AUMSPUB(SUBREG)                        PUBLIC FIXED PRICE AUMS SUPPLY
AUMSPRIV(SUBREG)                       PRIVATE AUMS SUPPLY
AUMSPRIVS(SUBREG,STEPS)                AUM SUPPLY BY STEP
WATERFIX(SUBREG)                       FIXED PRICE - FIXED WATER SUPPLY
WATERVAR(SUBREG)                       VARIABLE COST PUMPED WATER SUPPLY
WATERVARS(SUBREG,STEPS)                WATER SUPPLY BY STEP
FAMILY(REGIONS)                        FAMILY LABOR SUPPLY
HIRED(REGIONS)                         HIRED LABOR SUPPLY
HIREDS(REGIONS,STEPS)                  LABOR SUPPLY BY STEP
MIXR(SUBREG,CRPMIXALT)                 CROP MIXES BY REGION
NATMIX(PRIMARY,NATMIXALT)              PRIMARY PRODUCT MIXACROSS SUBREGS
CCCLOANP(PRIMARY)                      PRIMARY PRODUCT CCC LOAN DEMAND
CCCLOANS(SECONDARY)                    SECONDARY PRODUCT CCC LOAN DEMAND
DEFPRODN(ALLI)                         PRODUCTION RECEIVE DEFIC PYMT
PRDN5092(ALLI)                         PRODUCTION RECEIVE 5092 PYMT
PRDN092(ALLI)                          PRODUCTION RECEIVE 092 PYMT
DIVPRODN(ALLI)                         PRODUCTION RECEIVE DIVERSION PYMT
ARTIF(ALLI)                            ARTIFICIAL PRODUCTION
UNHARV(ALLI)                           UNHARVESTED PRODUCTION
ARTS(alli)                             ARTIFICIAL SUPPLY
CROPBUDGET(SUBREG,CROP,WTECH,CTECH,TECH)  CROP BUDGETS
LVSTBUDGET(SUBREG,ANIMAL,LIVETECH)     LIVESTOCK BUDGETS
PROCESS(PROCESSALT)                    PROCESSING BUDGETS
 ;

VARIABLES

CSPS
TOLR(PRIMARY,SUBREG)                   NATIONAL CROP MIX TOLERENCE
TWID(CROP,SUBREG)                      CROP MIXES TOLERENCE

EQUATIONS

    OBJT
    PRIMARYBAL(PRIMARY)           PRIMARY PRODUCT BALANCE
    SECONDBAL(SECONDARY)          SECONDARY PRODUCT BALANCE
    MAXLAND(SUBREG,LANDTYPE)      MAXIMUM LAND AVAILABLE IN A SUBREG
    LAND(SUBREG,LANDTYPE)         REGIONAL LAND BALANCE
    PUBAUMS(SUBREG)               MAXIMUM FIXED PRICE PUBLIC AUMS
    AUMSR(SUBREG)                 SUBREGIONAL AUMS BALANCE
    WATERR(SUBREG)                SUBREGIONAL WATER BALANCE
    FIX(SUBREG)                   SUBREGIONAL MAXIMUM FIXED PRICE WATER
    LABOR(REGIONS)                REGIONAL LABOR BALANCE
    FAMILYLIM(REGIONS)            MAXIMUM FAMILY LABOR
    HIRELIM(REGIONS)              MAXIMUM HIRED LABOR
    MIXREG(CROP,SUBREG)           CROP MIX BY ACREAGE IN SUBREGION
    MIXREGTOT(SUBREG)             TOTAL ACRES IN A CROP MIX
    MIXNAT(PRIMARY,SUBREG)        PRIMARY DISTRIBUTION ACCROSS SUBREGS
    FRMPROG(CROP)                 TOTAL FARM PROGRAM PRODUCTION
    P5092(CROP)                   TOTAL 5092 ACREAGE PRODUCTION
    P092(CROP)                    TOTAL 092 ACREAGE PRODUCTION
    DIVERT(CROP)                  TOTAL VOLUNTARY DIVERSION PRODUCTION
    ARTIFICIAL(CROP)              ARTIFICIAL FARM PROGRAM PRODUCTION
    UNHARVEST(CROP)               PAID BUT UNHARVESTED FARM PROG PROD

$if "%solveasm%" == "nonlinear"   $goto bounds
    PDEMIDENT(PRIMARY)            IDENTITY FOR PRIMARY   PRODUCTS
    PEXPIDENT(PRIMARY)            IDENTITY FOR PRIMARY   PRODUCTS
    PIMPIDENT(PRIMARY)            IDENTITY FOR PRIMARY   PRODUCTS
    SDEMIDENT(SECONDARY)          IDENTITY FOR SECONDARY PRODUCTS
    SEXPIDENT(SECONDARY)          IDENTITY FOR SECONDARY PRODUCTS
    SIMPIDENT(SECONDARY)          IDENTITY FOR SECONDARY PRODUCTS
    LANDIDENT(LANDTYPE,SUBREG)    LAND  IDENTITY
    AUMSIDENT(SUBREG)             AUMS  IDENTITY
    WATRIDENT(SUBREG)             WATER IDENTITY
    LABRIDENT(REGIONS)            LABOR IDENTITY
    DEMPCONVEX(PRIMARY)           PRIMARY   PRODUCT DEMAND CONVEXITY
    EXPPCONVEX(PRIMARY)           PRIMARY   PRODUCT EXPORT CONVEXITY
    IMPPCONVEX(PRIMARY)           PRIMARY   PRODUCT IMPORT CONVEXITY
    DEMSCONVEX(SECONDARY)         SECONDARY PRODUCT DEMAND CONVEXITY
    EXPSCONVEX(SECONDARY)         SECONDARY PRODUCT EXPORT CONVEXITY
    IMPSCONVEX(SECONDARY)         SECONDARY PRODUCT IMPORT CONVEXITY
    LANDCONVEX(LANDTYPE,SUBREG)   LAND  CONVEXITY
    AUMSCONVEX(SUBREG)            AUMS  CONVEXITY
    WATRCONVEX(SUBREG)            WATER CONVEXITY
    LABRCONVEX(REGIONS)           LABOR CONVEXITY
$goto bounds

*   THE FOLLOWING ARE IMPOSED BY BOUNDS

$label bounds
*    IMPORTMAX(PRIMARY)          PRIMARY COMMODITY IMPORT MAXIMUM
*    IMPORTMIN(PRIMARY)          PRIMARY COMMODITY IMPORT MINIMUM
*    EXPORTMAX(PRIMARY)          PRIMARY COMMODITY EXPORT MAXIMUM
*    EXPORTMIN(PRIMARY)          PRIMARY COMMODITY EXPORT MINIMUM
*    DEMANDMAX(PRIMARY)          PRIMARY COMMODITY DEMAND MAXIMUM
*    DEMANDMIN(PRIMARY)          PRIMARY COMMODITY DEMAND MINIMUM
*    IMPORTMAX(SECONDARY)        SECONDARY COMMODITY IMPORT MAXIMUM
*    IMPORTMIN(SECONDARY)        SECONDARY COMMODITY IMPORT MINIMUM
*    EXPORTMAX(SECONDARY)        SECONDARY COMMODITY EXPORT MAXIMUM
*    SEXPORTMIN(SECONDARY)       SECONDARY COMMODITY EXPORT MINIMUM
*    SDEMANDMAX(SECONDARY)       SECONDARY COMMODITY DEMAND MAXIMUM
*    SDEMANDMIN(SECONDARY)       SECONDARY COMMODITY DEMAND MINIMUM
*    WATERMIN(SUBREG)            WATER SUPPLY MINIMUM TO AVOID NUMERIC PROBLEM
*    HIREMIN(REGION)            HIRED LABOR MINIMUM TO AVOID NUMERIC PROBLEM

    ;

  OBJT..  CSPS =E=

( SUM(PRIMARY$(PDEMAND(PRIMARY,"QUANTITY")   GT 0 AND
               PDEMAND(PRIMARY,"PRICE")      GT 0 AND
               PDEMAND(PRIMARY,"ELASTICITY") LT -0.05     ),

$if not "%solveasm%" == "nonlinear"      $goto linear1
$if     "%solveasm%" == "nonlinear"      $goto nonlinear1

$label nonlinear1
   ((DEMANDP(PRIMARY)/PDEMAND(PRIMARY,"QUANTITY"))
                      **(1./PDEMAND(PRIMARY,"ELASTICITY"))
   * DEMANDP(PRIMARY) * PDEMAND(PRIMARY,"PRICE")
   * PDEMAND(PRIMARY,"ELASTICITY")  / (1.+PDEMAND(PRIMARY,"ELASTICITY"))
   + PDEMAND(PRIMARY,"CONSTANT1")+PDEMAND(PRIMARY,"CONSTANT2")))
$goto continue1

$label linear1
   + (SUM(STEPS$(PDEMAND(PRIMARY,"TFAC") GE 1/QINC(STEPS)),
            (QINC(STEPS) **(1./PDEMAND(PRIMARY,"ELASTICITY"))
   * PDEMAND(PRIMARY,"QUANTITY")*QINC(STEPS)   * PDEMAND(PRIMARY,"PRICE")
   * PDEMAND(PRIMARY,"ELASTICITY")   / (1.+PDEMAND(PRIMARY,"ELASTICITY"))
   +PDEMAND(PRIMARY,"CONSTANT1")   +PDEMAND(PRIMARY,"CONSTANT2")
    )*DEMANDPS(PRIMARY,STEPS))
    )    )
$goto continue1

$label continue1
+SUM(PRIMARY$(PDEMAND(PRIMARY,"PRICE")      GT 0 AND
               PDEMAND(PRIMARY,"ELASTICITY") GE -0.05
              AND PDEMAND(PRIMARY,"QUANTITY") NE 0   ),
               PDEMAND(PRIMARY,"PRICE")*DEMANDP(PRIMARY))

+SUM(PRIMARY$(PEXPORT(PRIMARY,"QUANTITY")   GT 0 AND
             PEXPORT(PRIMARY,"PRICE")      GT 0 AND
             PEXPORT(PRIMARY,"ELASTICITY") LT -0.05     ),

$if not "%solveasm%" == "nonlinear"      $goto linear2
$if     "%solveasm%" == "nonlinear"      $goto nonlinear2

$label nonlinear2
   ((EXPORTP(PRIMARY)/PEXPORT(PRIMARY,"QUANTITY"))
                      **(1./PEXPORT(PRIMARY,"ELASTICITY"))
   * EXPORTP(PRIMARY) * PEXPORT(PRIMARY,"PRICE")
   * PEXPORT(PRIMARY,"ELASTICITY") / (1.+PEXPORT(PRIMARY,"ELASTICITY"))
   +PEXPORT(PRIMARY,"CONSTANT1")+PEXPORT(PRIMARY,"CONSTANT2")  ))
$goto continue2


$label linear2
   + (SUM(STEPS$(PEXPORT(PRIMARY,"TFAC") GE 1/QINC(STEPS)),
                 (QINC(STEPS) **(1./PEXPORT(PRIMARY,"ELASTICITY"))
   * PEXPORT(PRIMARY,"QUANTITY")*QINC(STEPS) * PEXPORT(PRIMARY,"PRICE")
   * PEXPORT(PRIMARY,"ELASTICITY") / (1.+PEXPORT(PRIMARY,"ELASTICITY"))
   + PEXPORT(PRIMARY,"CONSTANT1") +PEXPORT(PRIMARY,"CONSTANT2"))
   * EXPORTPS(PRIMARY,STEPS)  )     ))
$goto continue2

$label continue2
+SUM(PRIMARY$(PEXPORT(PRIMARY,"PRICE")      GT 0 AND
               PEXPORT(PRIMARY,"ELASTICITY") GE -0.05
              AND PEXPORT(PRIMARY,"QUANTITY") NE 0    ),
               PEXPORT(PRIMARY,"PRICE")*EXPORTP(PRIMARY) )

-SUM(PRIMARY$(PIMPORT(PRIMARY,"PRICE")      GT 0 AND
              PIMPORT(PRIMARY,"QUANTITY")   GT 0 AND
             PIMPORT(PRIMARY,"ELASTICITY") GT 0.05     ),
   +PIMPORT(PRIMARY,"ELASTICITY") /(1.+PIMPORT(PRIMARY,"ELASTICITY"))
  *(PIMPORT(PRIMARY,"PRICE")
  *(

$if not "%solveasm%" == "nonlinear"      $goto linear3
$if     "%solveasm%" == "nonlinear"      $goto nonlinear3

$label nonlinear3
((IMPORTP(PRIMARY)/PIMPORT(PRIMARY,"QUANTITY"))**
                  (1./PIMPORT(PRIMARY,"ELASTICITY"))
               * IMPORTP(PRIMARY)) )))
$goto continue3

$label linear3
      +SUM(STEPS,(QINC(STEPS))** (1./PIMPORT(PRIMARY,"ELASTICITY"))
               * QINC(STEPS)*PIMPORT(PRIMARY,"QUANTITY")
              *IMPORTPS(PRIMARY,STEPS)
  )  )))
$goto continue3

$label continue3
-SUM(PRIMARY$(PIMPORT(PRIMARY,"PRICE")      GT 0 AND
               PIMPORT(PRIMARY,"ELASTICITY") LE 0.05
              AND PIMPORT(PRIMARY,"QUANTITY") NE 0      ),
               PIMPORT(PRIMARY,"PRICE")*IMPORTP(PRIMARY))

+SUM(SECONDARY$(SDEMAND(SECONDARY,"QUANTITY")   GT 0 AND
             SDEMAND(SECONDARY,"PRICE")      GT 0 AND
             SDEMAND(SECONDARY,"ELASTICITY") LT -0.05     ),

$if not "%solveasm%" == "nonlinear"      $goto linear4
$if     "%solveasm%" == "nonlinear"      $goto nonlinear4

$label nonlinear4
  ( (DEMANDS(SECONDARY)/SDEMAND(SECONDARY,"QUANTITY"))
                      **(1./SDEMAND(SECONDARY,"ELASTICITY"))
  * DEMANDS(SECONDARY)   * SDEMAND(SECONDARY,"PRICE")
  * SDEMAND(SECONDARY,"ELASTICITY")   / (1.+SDEMAND(SECONDARY,"ELASTICITY"))
  +SDEMAND(SECONDARY,"CONSTANT1")+SDEMAND(SECONDARY,"CONSTANT2")))
$goto continue4

$label linear4
   + (SUM(STEPS$(SDEMAND(SECONDARY,"TFAC") GE 1/QINC(STEPS)),
            (QINC(STEPS) **(1./SDEMAND(SECONDARY,"ELASTICITY"))
   * SDEMAND(SECONDARY,"QUANTITY")*QINC(STEPS)   * SDEMAND(SECONDARY,"PRICE")
   * SDEMAND(SECONDARY,"ELASTICITY")   / (1.+SDEMAND(SECONDARY,"ELASTICITY"))
   +SDEMAND(SECONDARY,"CONSTANT1")   +SDEMAND(SECONDARY,"CONSTANT2")
    )*DEMANDSS(SECONDARY,STEPS))
    )    )
$goto continue4

$label continue4
+SUM(SECONDARY$(SDEMAND(SECONDARY,"PRICE")      GT 0 AND
               SDEMAND(SECONDARY,"ELASTICITY") GE -0.05
              AND SDEMAND(SECONDARY,"QUANTITY") NE 0   ),
               SDEMAND(SECONDARY,"PRICE")*DEMANDS(SECONDARY))


+SUM(SECONDARY$(SEXPORT(SECONDARY,"QUANTITY")   GT 0 AND
                SEXPORT(SECONDARY,"PRICE")      GT 0 AND
                SEXPORT(SECONDARY,"ELASTICITY") LT -0.05     ),

$if not "%solveasm%" == "nonlinear"      $goto linear5
$if     "%solveasm%" == "nonlinear"      $goto nonlinear5

$label nonlinear5
   ((EXPORTS(SECONDARY)/SEXPORT(SECONDARY,"QUANTITY"))
                      **(1./SEXPORT(SECONDARY,"ELASTICITY"))
   * EXPORTS(SECONDARY)   * SEXPORT(SECONDARY,"PRICE")
   * SEXPORT(SECONDARY,"ELASTICITY") / (1.+SEXPORT(SECONDARY,"ELASTICITY"))
   +SEXPORT(SECONDARY,"CONSTANT1") +SEXPORT(SECONDARY,"CONSTANT2")  )
   )
$goto continue5

$label linear5
   + (SUM(STEPS$(SEXPORT(SECONDARY,"TFAC") GE 1/QINC(STEPS)),
                 (QINC(STEPS) **(1./SEXPORT(SECONDARY,"ELASTICITY"))
   * SEXPORT(SECONDARY,"QUANTITY")*QINC(STEPS)
   * SEXPORT(SECONDARY,"PRICE")   * SEXPORT(SECONDARY,"ELASTICITY")
   / (1.+SEXPORT(SECONDARY,"ELASTICITY"))
      +SEXPORT(SECONDARY,"CONSTANT1") +SEXPORT(SECONDARY,"CONSTANT2"))
   *EXPORTSS(SECONDARY,STEPS)  )
  ))
$goto continue5

$label continue5
+SUM(SECONDARY$(SEXPORT(SECONDARY,"PRICE")      GT 0 AND
               SEXPORT(SECONDARY,"ELASTICITY") GE -0.05
              AND SEXPORT(SECONDARY,"QUANTITY") NE 0    ),
               SEXPORT(SECONDARY,"PRICE")*EXPORTS(SECONDARY))

-SUM(SECONDARY$(SIMPORT(SECONDARY,"PRICE")      GT 0 AND
              SIMPORT(SECONDARY,"QUANTITY")   GT 0 AND
             SIMPORT(SECONDARY,"ELASTICITY") GT 0.05     ),
   +SIMPORT(SECONDARY,"ELASTICITY") /(1.+SIMPORT(SECONDARY,"ELASTICITY"))
  *(SIMPORT(SECONDARY,"PRICE")
  *(

$if not "%solveasm%" == "nonlinear"      $goto linear6
$if     "%solveasm%" == "nonlinear"      $goto nonlinear6

$label nonlinear6
((IMPORTS(SECONDARY)/SIMPORT(SECONDARY,"QUANTITY"))**
                  (1./SIMPORT(SECONDARY,"ELASTICITY"))
               * IMPORTS(SECONDARY)) )))
$goto continue6

$label linear6
      +SUM(STEPS,(QINC(STEPS))** (1./SIMPORT(SECONDARY,"ELASTICITY"))
               * QINC(STEPS)*SIMPORT(SECONDARY,"QUANTITY")
              *IMPORTSS(SECONDARY,STEPS)
  )  )))
$goto continue6

$label continue6
-SUM(SECONDARY$(SIMPORT(SECONDARY,"PRICE")      GT 0 AND
               SIMPORT(SECONDARY,"ELASTICITY") LE 0.05
              AND SIMPORT(SECONDARY,"QUANTITY") NE 0     ),
               SIMPORT(SECONDARY,"PRICE")*IMPORTS(SECONDARY))

-SUM((LANDTYPE,SUBREG)$(LANDSUPPL(LANDTYPE,SUBREG,"QUANTITY")   GT 0 AND
                          LANDSUPPL(LANDTYPE,SUBREG,"PRICE")      GT 0 AND
                          LANDSUPPL(LANDTYPE,SUBREG,"ELASTICITY") GT 0.05  ),
  +LANDSUPPL(LANDTYPE,SUBREG,"ELASTICITY")
  /(1.+LANDSUPPL(LANDTYPE,SUBREG,"ELASTICITY"))
  *(LANDSUPPL(LANDTYPE,SUBREG,"PRICE")
  *(

$if not "%solveasm%" == "nonlinear"      $goto linear7
$if     "%solveasm%" == "nonlinear"      $goto nonlinear7

$label nonlinear7
((LANDSUPPLY(SUBREG,LANDTYPE)
      /LANDSUPPL(LANDTYPE,SUBREG,"QUANTITY"))**
                  (1./LANDSUPPL(LANDTYPE,SUBREG,"ELASTICITY"))
               * LANDSUPPLY(SUBREG,LANDTYPE)) )))
$goto continue7

$label linear7
      +SUM(STEPS,(QINC(STEPS))**
                 (1./LANDSUPPL(LANDTYPE,SUBREG,"ELASTICITY"))
               * QINC(STEPS)*LANDSUPPL(LANDTYPE,SUBREG,"QUANTITY")
              *LANDSUPPLS(SUBREG,LANDTYPE,STEPS) )   )))
$goto continue7

$label continue7
-SUM((SUBREG,LANDTYPE)$(LANDSUPPL(LANDTYPE,SUBREG,"PRICE")      GT 0 AND
                          LANDSUPPL(LANDTYPE,SUBREG,"ELASTICITY") LE 0.05 AND
                          LANDSUPPL(LANDTYPE,SUBREG,"QUANTITY") NE 0.0  ),
                          LANDSUPPL(LANDTYPE,SUBREG,"PRICE")*
                          LANDSUPPLY(SUBREG,LANDTYPE))

-SUM(SUBREG$( AUMSSUP(SUBREG,"PRIVATEQ")      GT 0 AND
                AUMSSUP(SUBREG,"PRIVATEP")  GT 0 AND
                AUMSSUP(SUBREG,"PRIVATELAS")   GT 0.05   ),
    AUMSSUP(SUBREG,"PRIVATELAS")/(1.+AUMSSUP(SUBREG,"PRIVATELAS"))
    *(AUMSSUP(SUBREG,"PRIVATEP")
    *(

$if not "%solveasm%" == "nonlinear"      $goto linear8
$if     "%solveasm%" == "nonlinear"      $goto nonlinear8

$label nonlinear8
((AUMSPRIV(SUBREG)/AUMSSUP(SUBREG,"PRIVATEQ"))**
                  (1./AUMSSUP(SUBREG,"PRIVATELAS"))
    * AUMSPRIV(SUBREG)) )))
$goto continue8

$label linear8
    +SUM(STEPS,(QINC(STEPS))**(1./AUMSSUP(SUBREG,"PRIVATELAS"))
               * QINC(STEPS)*AUMSSUP(SUBREG,"PRIVATEQ")
              *AUMSPRIVS(SUBREG,STEPS)   )                  )))
$goto continue8

$label continue8
-SUM(SUBREG$(AUMSSUP(SUBREG,"PRIVATEP")  GT 0 AND
               AUMSSUP(SUBREG,"PRIVATELAS")   LE 0.05 AND
               AUMSSUP(SUBREG,"PRIVATEQ") NE 0.0     ),
               AUMSSUP(SUBREG,"PRIVATEP")* AUMSPRIV(SUBREG))

- SUM(SUBREG,AUMSPUB(SUBREG)* AUMSSUP(SUBREG,"PUBLICPRC"))

-SUM(SUBREG$( WATERSUP(SUBREG,"PUMPQ")      GT 0 AND
                WATERSUP(SUBREG,"PUMPPRICE")  GT 0 AND
                WATERSUP(SUBREG,"PUMPELAS")   GT 0.05   ),
    WATERSUP(SUBREG,"PUMPELAS")   /(1.+WATERSUP(SUBREG,"PUMPELAS"))
    *(WATERSUP(SUBREG,"PUMPPRICE")
  *(

$if not "%solveasm%" == "nonlinear"      $goto linear9
$if     "%solveasm%" == "nonlinear"      $goto nonlinear9

$label nonlinear9
((WATERVAR(SUBREG)/WATERSUP(SUBREG,"PUMPQ"))**
                  (1./WATERSUP(SUBREG,"PUMPELAS"))
               * WATERVAR(SUBREG)) )))
$goto continue9

$label linear9
      +SUM(STEPS,(QINC(STEPS))**
                 (1./WATERSUP(SUBREG,"PUMPELAS"))
               * QINC(STEPS)*WATERSUP(SUBREG,"PUMPQ")
              *WATERVARS(SUBREG,STEPS)
     )
                  )))
$goto continue9

$label continue9
-SUM(SUBREG$(WATERSUP(SUBREG,"PUMPPRICE")  GT 0 AND
               WATERSUP(SUBREG,"PUMPELAS")   LE 0.05 AND
               WATERSUP(SUBREG,"PUMPQ") NE 0.0     ),
               WATERSUP(SUBREG,"PUMPPRICE")*
               WATERVAR(SUBREG))

-SUM(REGION$(LABORSUP(REGION,"HIREQ")      GT 0 AND
               LABORSUP(REGION,"HIREP")      GT 0 AND
               LABORSUP(REGION,"HIREELAS")   GT 0.05     ),
          LABORSUP(REGION,"HIREELAS") /(1.+LABORSUP(REGION,"HIREELAS"))
                *(LABORSUP(REGION,"HIREP")
  *(

$if not "%solveasm%" == "nonlinear"      $goto linear0
$if     "%solveasm%" == "nonlinear"      $goto nonlinear0

$label nonlinear0
((HIRED(REGION)/LABORSUP(REGION,"HIREQ"))**
                  (1./LABORSUP(REGION,"HIREELAS"))
               * HIRED(REGION)) )))
$goto continue0

$label linear0
      +SUM(STEPS,(QINC(STEPS))**
                 (1./LABORSUP(REGION,"HIREELAS"))
               * QINC(STEPS)*LABORSUP(REGION,"HIREQ")
              *HIREDS(REGION,STEPS)    )         )))
$goto continue0

$label continue0
- SUM(REGION$(LABORSUP(REGION,"HIREQ")    LE 0 AND
                     LABORSUP(REGION,"HIREP")    GT 0 AND
                     LABORSUP(REGION,"HIREELAS") LE 0.05 AND
                     LABORSUP(REGION,"HIREQ") NE 0.0),
                     LABORSUP(REGION,"HIREP")*
                     HIRED(REGION))


- SUM(REGION,FAMILY(REGION)* LABORSUP(REGION,"FAMILYPRC"))

-(SUM(SUBREG,
     SUM(CROP$FARMPROD("SLIPPAGE",CROP),
        sum((WTECH,TECH),
           costc(SUBREG,CROP,WTECH,"particip",TECH)*
           CROPBUDGET(SUBREG,CROP,WTECH,"PARTICIP",TECH)
          +costc(SUBREG,CROP,WTECH,"nonpart",TECH)*
           CROPBUDGET(SUBREG,CROP,WTECH,"nonpart",TECH)))
    +SUM(CROP$(FARMPROD("SLIPPAGE",CROP) LE 0.0),
        sum((WTECH,TECH),
           costc(SUBREG,CROP,WTECH,"base",TECH)*
           CROPBUDGET(SUBREG,CROP,WTECH,"base",TECH)))
    +SUM((ANIMAL,LIVETECH),
        costl(SUBREG,ANIMAL,LIVETECH)*
        LVSTBUDGET(SUBREG,ANIMAL,LIVETECH)))+
     SUM(INPUT$INPUTPRICE(INPUT),INPUTPRICE(INPUT)*
        SUM(PROCESSALT,PROCESS(PROCESSALT)*PROCBUD(INPUT,PROCESSALT))))

- SUM(PROCESSALT,PROCESS(PROCESSALT)*
           (SUM(COST,PROCBUD(COST,PROCESSALT))))

- SUM(SUBREG,WATERFIX(SUBREG)* WATERSUP(SUBREG,"FIXEDPRC"))

+ SUM(CROP$(FARMPROD("TARGET",CROP) GT 0 AND
            FARMPROD("SLIPPAGE",CROP) GT 0 AND
            FARMPROD("MKTLOANY-N",CROP) GT 0),
  (FARMPROD("DEFIC",CROP)+FARMPROD("MKTLOANY-N",CROP)*FARMPROD("MKTLOAN",CROP))
    *DEFPRODN(CROP)     )

+ SUM(CROP$(FARMPROD("TARGET",CROP) GT 0 AND
            FARMPROD("SLIPPAGE",CROP) GT 0 AND
            FARMPROD("MKTLOANY-N",CROP) LE 0),
  (FARMPROD("DEFIC",CROP)*DEFPRODN(CROP)   )   )

+ SUM(CROP$(FARMPROD("TARGET",CROP)*FARMPROD("50-92",CROP)),
        FARMPROD("DEFIC",CROP)*PRDN5092(CROP) )

+ SUM(CROP$(FARMPROD("TARGET",CROP)*FARMPROD("0-92",CROP)),
        FARMPROD("DEFIC",CROP)*PRDN092(CROP) )

+ SUM(CROP$(FARMPROD("TARGET",CROP)*FARMPROD("diversion",CROP)),
        FARMPROD("DIVERPAY",CROP)*DIVPRODN(CROP)  )

+ SUM(CROP$(FARMPROD("TARGET",CROP)  GT 0 AND
            FARMPROD("FPYIELD",CROP) GT 1.0),
        FARMPROD("DEFIC",CROP)*ARTIF(CROP)    )

+ SUM(CROP$(FARMPROD("TARGET",CROP) GT 0 AND
              FARMPROD("UNHARVACR",CROP) GT 0),
        FARMPROD("DEFIC",CROP)*UNHARV(CROP)    )

+ SUM(PRIMARY$(FARMPROD("LOANRATE",PRIMARY) GT 0 AND
               FARMPROD("MKTLOANY-N",PRIMARY) LT 1.0),
       FARMPROD("LOANRATE",PRIMARY)*CCCLOANP(PRIMARY) )

+ SUM(SECONDARY$(FARMPROD("LOANRATE",SECONDARY) GT 0 AND
               FARMPROD("MKTLOANY-N",SECONDARY) LT 1.0),
       FARMPROD("LOANRATE",SECONDARY)*CCCLOANS(SECONDARY) ))

-SUM(prIMARY,999999*ARTS(PRIMARY))
-SUM(secondARY,999999*ARTS(secondARY))
  ;

 PRIMARYBAL(PRIMARY)..
        - SUM(SUBREG,
           SUM((CROP,WTECH,TECH)$FARMPROD("SLIPPAGE",CROP),
              CROPBUDGET(SUBREG,CROP,WTECH,"NONPART",TECH)

               *CCCBUDDATA(PRIMARY,SUBREG,CROP,WTECH,"NONPART",TECH)
             +CROPBUDGET(SUBREG,CROP,WTECH,"PARTICIP",TECH)

               *CCCBUDDATA(PRIMARY,SUBREG,CROP,WTECH,"NONPART",TECH)
               *(1.0-FPPART(SUBREG,CROP))
             + CROPBUDGET(SUBREG,CROP,WTECH,"PARTICIP",TECH)
              *CCCBUDDATA(PRIMARY,SUBREG,CROP,WTECH,"PARTICIP",TECH)
              *FPPART(SUBREG,CROP)
              * (1.0-FARMPROD("PERCNTPAID",CROP))
             + CROPBUDGET(SUBREG,CROP,WTECH,"PARTICIP",TECH)
              *CCCBUDDATA(PRIMARY,SUBREG,CROP,WTECH,"PARTICIP",TECH)
              *FPPART(SUBREG,CROP)*farmprod("flex",crop)
              * (FARMPROD("PERCNTPAID",CROP))    )

         + SUM((CROP,WTECH,TECH)$(FARMPROD("SLIPPAGE",CROP) GT 0.0 AND
                       FARMPROD("FPYIELD",CROP) LE 1.0),
                CROPBUDGET(SUBREG,CROP,WTECH,"PARTICIP",TECH)
                *FPPART(SUBREG,CROP) * FARMPROD("PERCNTPAID",CROP)

                 *CCCBUDDATA(PRIMARY,SUBREG,CROP,WTECH,"PARTICIP",TECH)
                 *(1.0-FARMPROD("FPYIELD",CROP))    )
         + SUM((CROP,WTECH,TECH)$(FARMPROD("SLIPPAGE",CROP) LE 0.),
                 CROPBUDGET(SUBREG,CROP,WTECH,"BASE",TECH)

                *CCCBUDDATA(PRIMARY,SUBREG,CROP,WTECH,"BASE",TECH) )

         + SUM((ANIMAL,LIVETECH),
                 LVSTBUDGET(SUBREG,ANIMAL,LIVETECH)

                *LBUDDATA(PRIMARY,SUBREG,ANIMAL,LIVETECH))
               )
      -DEFPRODN(PRIMARY)$(FARMPROD("SLIPPAGE",PRIMARY) GT 0 AND
                          FARMPROD("TARGET",PRIMARY) GT 0 )
      +DEMANDP(PRIMARY)$(PDEMAND(PRIMARY,"QUANTITY")+PDEMAND(PRIMARY,"MINQ"))
      -ARTS(PRIMARY)$PDEMAND(PRIMARY,"MINQ")
      -IMPORTP(PRIMARY)$PIMPORT(PRIMARY,"QUANTITY")
      +EXPORTP(PRIMARY)$PEXPORT(PRIMARY,"QUANTITY")
      +SUM(PROCESSALT,PROCESS(PROCESSALT)*PROCBUD(PRIMARY,PROCESSALT))

      +CCCLOANP(PRIMARY)$(FARMPROD("LOANRATE",PRIMARY) GT 0  AND
                    FARMPROD("MKTLOANY-N",PRIMARY) LT 1.0)
          =L= 0. ;

*SEPERABLE PROGRAMING CONVEXITY CONSTRAINTS

$if     "%solveasm%" == "nonlinear"      $goto continue11

PDEMIDENT(PRIMARY)$( PDEMAND(PRIMARY,"ELASTICITY")
   LT -0.05)..
   SUM(STEPS,QINC(STEPS)*PDEMAND(PRIMARY,"QUANTITY")*DEMANDPS(PRIMARY,STEPS))
    =l= DEMANDP(PRIMARY);

DEMPCONVEX(PRIMARY)$( PDEMAND(PRIMARY,"elasticity")
  lT -0.05)..
              SUM(STEPS,DEMANDPS(PRIMARY,STEPS)) =L= 1.;

 PEXPIDENT(PRIMARY)$( PEXPORT(PRIMARY,"ELASTICITY") LT -0.05)..
   SUM(STEPS,QINC(STEPS)*PEXPORT(PRIMARY,"QUANTITY")*EXPORTPS(PRIMARY,STEPS))
    =l= EXPORTP(PRIMARY);

EXPPCONVEX(PRIMARY)$( PEXPORT(PRIMARY,"elasticity") lt 0)..
                 SUM(STEPS,EXPORTPS(PRIMARY,STEPS)) =L= 1.;

 PIMPIDENT(PRIMARY)$( PIMPORT(PRIMARY,"ELASTICITY") GT 0.05)..
   -SUM(STEPS,QINC(STEPS)*PIMPORT(PRIMARY,"QUANTITY")*IMPORTPS(PRIMARY,STEPS))
    =l= -IMPORTP(PRIMARY);

IMPPCONVEX(PRIMARY)$( PIMPORT(PRIMARY,"elasticity") GT 0)..
               SUM(STEPS,IMPORTPS(PRIMARY,STEPS)) =L= 1.;

 LANDIDENT(LANDTYPE,SUBREG)
    $( LANDSUPPL(LANDTYPE,SUBREG,"ELASTICITY") GT 0.05)..
   -SUM(STEPS,QINC(STEPS)*LANDSUPPL(LANDTYPE,SUBREG,"QUANTITY")
          *LANDSUPPLS(SUBREG,LANDTYPE,STEPS))
    =l= -LANDSUPPLY(SUBREG,LANDTYPE);

  LANDCONVEX(LANDTYPE,SUBREG)
     $( LANDSUPPL(LANDTYPE,SUBREG,"elasticITY") GT 0)..
              SUM(STEPS,LANDSUPPLS(SUBREG,LANDTYPE,STEPS)) =L= 1.;

 WATRIDENT(SUBREG)
    $( WATERSUP(SUBREG,"PUMPELAS") GT 0.05)..
   -SUM(STEPS,QINC(STEPS)*WATERSUP(SUBREG,"PUMPQ")
          *WATERVARS(SUBREG,STEPS))
    =l= -WATERVAR(SUBREG);

   WATRCONVEX(SUBREG)$( WATERSUP(SUBREG,"pumpelas") GT 0)..
                 SUM(STEPS,WATERVARS(SUBREG,STEPS)) =L= 1.;

 LABRIDENT(REGION)
    $( LABORSUP(REGION,"HIREELAS") GT 0.05)..
   -SUM(STEPS,QINC(STEPS)*LABORSUP(REGION,"HIREQ")
          *HIREDS(REGION,STEPS))
    =l= -HIRED(REGION);

   LABRCONVEX(REGION)$( LABORSUP(REGION,"HIREelas") GT 0)..
                 SUM(STEPS,HIREDS(REGION,STEPS)) =L= 1.;
 AUMSIDENT(SUBREG)
    $( AUMSSUP(SUBREG,"PRIVATELAS") GT 0.05)..
   -SUM(STEPS,QINC(STEPS)*AUMSSUP(SUBREG,"PRIVATEQ")
          *AUMSPRIVS(SUBREG,STEPS))
    =l= -AUMSPRIV(SUBREG);

   AUMSCONVEX(SUBREG)$( AUMSSUP(SUBREG,"PRIVATElas") GT 0)..
                 SUM(STEPS,AUMSPRIVS(SUBREG,STEPS)) =L= 1.;
$goto continue11

$label continue11
 FRMPROG(CROP)$FARMPROD("TARGET",CROP)..
  - SUM((SUBREG,WTECH,TECH), CROPBUDGET(SUBREG,CROP,WTECH,"PARTICIP",TECH)

        *  FPPART(SUBREG,CROP) * FARMPROD("PERCNTPAID",CROP)*(1-farmprod("flex",crop))
        * MIN( CCCBUDDATA(CROP,SUBREG,CROP,WTECH,"PARTICIP",TECH) ,
       (FARMPROD("FPYIELD",CROP)
        *CCCBUDDATA(CROP,SUBREG,CROP,WTECH,"PARTICIP",TECH))))

    +DEFPRODN(CROP)
      =L=  0.  ;

 P5092(CROP)$(FARMPROD("TARGET",CROP)* FARMPROD("50-92",CROP))..
   - SUM((SUBREG,WTECH,TECH), CROPBUDGET(SUBREG,CROP,WTECH,"PARTICIP",TECH)
            *FPPART(SUBREG,CROP)*FARMPROD("50-92",CROP)*(1-farmprod("flex",crop))
        *FARMPROD("FPYIELD",CROP)
       *CCCBUDDATA(CROP,SUBREG,CROP,WTECH,"PARTICIP",TECH) )  *0.42
   +PRDN5092(CROP)
     =L=  0.  ;

 P092(CROP)$(FARMPROD("TARGET",CROP)*FARMPROD("0-92",CROP))..
   - SUM((SUBREG,WTECH,TECH), CROPBUDGET(SUBREG,CROP,WTECH,"PARTICIP",TECH)
            *FPPART(SUBREG,CROP)*FARMPROD("0-92",CROP)*(1-farmprod("flex",crop))
        *FARMPROD("FPYIELD",CROP)
       *CCCBUDDATA(CROP,SUBREG,CROP,WTECH,"PARTICIP",TECH) )  *0.92
   +PRDN092(CROP)
     =L=  0.  ;

 DIVERT(CROP)$(FARMPROD("TARGET",CROP)*FARMPROD("diversion",CROP))..
   - SUM((SUBREG,WTECH,TECH), CROPBUDGET(SUBREG,CROP,WTECH,"PARTICIP",TECH)
           *FPPART(SUBREG,CROP)*FARMPROD("DIVERSION",CROP)
        *FARMPROD("FPYIELD",CROP)*(1-farmprod("flex",crop))
        *CCCBUDDATA(CROP,SUBREG,CROP,WTECH,"PARTICIP",TECH) )
  + DIVPRODN(CROP)
   =L=  0.  ;

 UNHARVEST(CROP)$(FARMPROD("TARGET",CROP) GT 0 AND
              FARMPROD("UNHARVACR",CROP) GT 0)..
   - SUM((SUBREG,WTECH,TECH), CROPBUDGET(SUBREG,CROP,WTECH,"PARTICIP",TECH)
          *FPPART(SUBREG,CROP) *FARMPROD("PERCNTPAID",CROP)
       *FARMPROD("UNHARVACR",CROP) *FARMPROD("FPYIELD",CROP)*(1-farmprod("flex",crop))
      *CCCBUDDATA(CROP,SUBREG,CROP,WTECH,"PARTICIP",TECH) )
   +UNHARV(CROP)
   =L=  0.  ;

 ARTIFICIAL(CROP)$(FARMPROD("TARGET",CROP) GT 0 AND
              FARMPROD("FPYIELD",CROP) GT 1.0 )..
   - SUM((SUBREG,WTECH,TECH), CROPBUDGET(SUBREG,CROP,WTECH,"PARTICIP",TECH)
          *FPPART(SUBREG,CROP)   *FARMPROD("PERCNTPAID",CROP)
      *(FARMPROD("FPYIELD",CROP)-1.0)*(1-farmprod("flex",crop))
      *CCCBUDDATA(CROP,SUBREG,CROP,WTECH,"PARTICIP",TECH) )
   +ARTIF(CROP)
   =L=  0.  ;

 SECONDBAL(SECONDARY)..
         +SUM(SUBREG,
             SUM((CROP,WTECH,TECH)$FARMPROD("SLIPPAGE",CROP),
                 CROPBUDGET(SUBREG,CROP,WTECH,"PARTICIP",TECH)
                *CCCBUDDATA(SECONDARY,SUBREG,CROP,WTECH,"PARTICIP",TECH)+
                 CROPBUDGET(SUBREG,CROP,WTECH,"NONPART",TECH)
                *CCCBUDDATA(SECONDARY,SUBREG,CROP,WTECH,"NONPART",TECH)  )

             +SUM((CROP,WTECH,TECH)$(FARMPROD("SLIPPAGE",CROP) LE 0.0),
                 CROPBUDGET(SUBREG,CROP,WTECH,"BASE",TECH)
                *CCCBUDDATA(SECONDARY,SUBREG,CROP,WTECH,"BASE",TECH)     )

             +SUM((ANIMAL,LIVETECH),  LVSTBUDGET(SUBREG,ANIMAL,LIVETECH)
                    *LBUDDATA(SECONDARY,SUBREG,ANIMAL,LIVETECH)  )
                 )
          +DEMANDS(SECONDARY)$SDEMAND(SECONDARY,"QUANTITY")
          -IMPORTS(SECONDARY)$SIMPORT(SECONDARY,"QUANTITY")
          +EXPORTS(SECONDARY)$SEXPORT(SECONDARY,"QUANTITY")
          -SUM(PROCESSALT,PROCESS(PROCESSALT)*PROCBUD(SECONDARY,PROCESSALT)
                )
          +CCCLOANS(SECONDARY)$(FARMPROD("LOANRATE",SECONDARY) GT 0  AND
                                 FARMPROD("MKTLOANY-N",SECONDARY) LT 1.0)
        -(ARTS(secondARY)$sdemand(secondary,"minq"))

          =L= 0. ;

*SEPERABLE PROGRAMING CONVEXITY CONSTRAINTS

$if     "%solveasm%" == "nonlinear"      $goto continue12

SDEMIDENT(SECONDARY)$( SDEMAND(SECONDARY,"ELASTICITY") LT -0.05)..
   SUM(STEPS,QINC(STEPS)*SDEMAND(SECONDARY,"QUANTITY")*DEMANDSS(SECONDARY,STEPS))
    =l= DEMANDS(SECONDARY);


 SEXPIDENT(SECONDARY)$( SEXPORT(SECONDARY,"ELASTICITY") LT -0.05)..
   SUM(STEPS,QINC(STEPS)*SEXPORT(SECONDARY,"QUANTITY")*EXPORTSS(SECONDARY,STEPS))
    =l= EXPORTS(SECONDARY);


 SIMPIDENT(SECONDARY)$( SIMPORT(SECONDARY,"ELASTICITY") GT 0.05)..
   -SUM(STEPS,QINC(STEPS)*SIMPORT(SECONDARY,"QUANTITY")*IMPORTSS(SECONDARY,STEPS))
    =l= -IMPORTS(SECONDARY);

   DEMSCONVEX(SECONDARY)$( SDEMAND(SECONDARY,"QUANTITY") GT 0)..
               SUM(STEPS,DEMANDSS(SECONDARY,STEPS)) =L= 1.;

   EXPSCONVEX(SECONDARY)$( SEXPORT(SECONDARY,"QUANTITY") GT 0)..
                SUM(STEPS,EXPORTSS(SECONDARY,STEPS)) =L= 1.;
   IMPSCONVEX(SECONDARY)$( SIMPORT(SECONDARY,"QUANTITY") GT 0)..
                  SUM(STEPS,IMPORTSS(SECONDARY,STEPS)) =L= 1.;
$goto continue12

$label continue12
 MAXLAND(SUBREG,LANDTYPE)$LANDAVAIL(SUBREG,LANDTYPE)..
        (SUM((CROP,WTECH,TECH)$FARMPROD("SLIPPAGE",CROP),
             CROPBUDGET(SUBREG,CROP,WTECH,"PARTICIP",TECH)
            *CCCBUDDATA(LANDTYPE,SUBREG,CROP,WTECH,"PARTICIP",TECH)+
             CROPBUDGET(SUBREG,CROP,WTECH,"NONPART",TECH)
            *CCCBUDDATA(LANDTYPE,SUBREG,CROP,WTECH,"NONPART",TECH)  ) +
         SUM((CROP,WTECH,TECH)$(FARMPROD("SLIPPAGE",CROP) LE 0.),
             CROPBUDGET(SUBREG,CROP,WTECH,"BASE",TECH)
            *CCCBUDDATA(LANDTYPE,SUBREG,CROP,WTECH,"BASE",TECH)     ) +
         SUM((ANIMAL,LIVETECH), LVSTBUDGET(SUBREG,ANIMAL,LIVETECH)
            *LBUDDATA(LANDTYPE,SUBREG,ANIMAL,LIVETECH)))
         =L=
         LANDAVAIL(SUBREG,LANDTYPE)
             ;

 LAND(SUBREG,LANDTYPE)$landavail(subreg,landtype)..
        (    SUM((CROP,WTECH,TECH)$FARMPROD("SLIPPAGE",CROP),
                 CROPBUDGET(SUBREG,CROP,WTECH,"PARTICIP",TECH)

                *CCCBUDDATA(LANDTYPE,SUBREG,CROP,WTECH,"PARTICIP",TECH)+
                 CROPBUDGET(SUBREG,CROP,WTECH,"NONPART",TECH)

                *CCCBUDDATA(LANDTYPE,SUBREG,CROP,WTECH,"NONPART",TECH)  ) +
             SUM((CROP,WTECH,TECH)$(FARMPROD("SLIPPAGE",CROP) LE 0.0),
                 CROPBUDGET(SUBREG,CROP,WTECH,"BASE",TECH)

                *CCCBUDDATA(LANDTYPE,SUBREG,CROP,WTECH,"BASE",TECH)     ) +
             SUM((ANIMAL,LIVETECH),
                 LVSTBUDGET(SUBREG,ANIMAL,LIVETECH)

                *LBUDDATA(LANDTYPE,SUBREG,ANIMAL,LIVETECH)))

        -LANDSUPPLY(SUBREG,LANDTYPE)
        =L= 0. ;

 AUMSR(SUBREG)$(AUMSSUP(SUBREG,"PUBLICMAX")+AUMSSUP(SUBREG,"PRIVATEQ")+
                 AUMSSUP(SUBREG,"PRIVATEMAX"))..

  SUM((ANIMAL,LIVETECH),
                 LVSTBUDGET(SUBREG,ANIMAL,LIVETECH)

                *LBUDDATA("AUMS",SUBREG,ANIMAL,LIVETECH))


  - AUMSPUB(SUBREG)$aumssup(subreg,"publicmax") -AUMSPRIV(SUBREG)
          =L= 0;

 PUBAUMS(SUBREG)$aumssup(subreg,"publicmax")..
          AUMSPUB(SUBREG)
          =L= AUMSSUP(SUBREG,"PUBLICMAX");

 WATERR(SUBREG)$(WATERSUP(SUBREG,"FIXEDMAX")+WATERSUP(SUBREG,"PUMPQ")+
                 WATERSUP(SUBREG,"PUMPMAX"))..
         (SUM((CROP,TECH)$FARMPROD("SLIPPAGE",CROP),
                 CROPBUDGET(SUBREG,CROP,"IRRIG","PARTICIP",TECH)

                *CCCBUDDATA("WATER",SUBREG,CROP,"IRRIG","PARTICIP",TECH)+
                 CROPBUDGET(SUBREG,CROP,"IRRIG","NONPART",TECH)
                *CCCBUDDATA("WATER",SUBREG,CROP,"IRRIG","NONPART",TECH)   ) +
             SUM((CROP,TECH)$(FARMPROD("SLIPPAGE",CROP) LE 0.0),
                 CROPBUDGET(SUBREG,CROP,"IRRIG","BASE",TECH)

                *CCCBUDDATA("WATER",SUBREG,CROP,"IRRIG","BASE",TECH)      ) +
             SUM((ANIMAL,LIVETECH),
                 LVSTBUDGET(SUBREG,ANIMAL,LIVETECH)

                *LBUDDATA("WATER",SUBREG,ANIMAL,LIVETECH)))
          - WATERFIX(SUBREG)$watersup(subreg,"fixedmax")
               -WATERVAR(SUBREG)
          =L= 0;

 FIX(SUBREG)$watersup(subreg,"fixedmax")..
          WATERFIX(SUBREG)
          =L= WATERSUP(SUBREG,"FIXEDMAX");

 LABOR(region)..
         ( SUM(SUBREG$MAPPING(region,SUBREG),
              SUM((CROP,WTECH,TECH)$FARMPROD("SLIPPAGE",CROP),
                  CROPBUDGET(SUBREG,CROP,WTECH,"PARTICIP",TECH)

                 *CCCBUDDATA("LABOR",SUBREG,CROP,WTECH,"PARTICIP",TECH)+
                  CROPBUDGET(SUBREG,CROP,WTECH,"NONPART",TECH)

                 *CCCBUDDATA("LABOR",SUBREG,CROP,WTECH,"NONPART",TECH)  ) +
              SUM((CROP,WTECH,TECH)$(FARMPROD("SLIPPAGE",CROP) LE 0.0),
                  CROPBUDGET(SUBREG,CROP,WTECH,"BASE",TECH)

                 *CCCBUDDATA("LABOR",SUBREG,CROP,WTECH,"BASE",TECH)     ) +
              SUM((ANIMAL,LIVETECH),
                  LVSTBUDGET(SUBREG,ANIMAL,LIVETECH)

                 *LBUDDATA("LABOR",SUBREG,ANIMAL,LIVETECH))))
        - FAMILY(region) -HIRED(region)
          =L= 0;

 FAMILYLIM(region)$LABORSUP(region,"FAMILYMAX")..
          FAMILY(region) =L= LABORSUP(region,"FAMILYMAX") ;

 HIRELIM(region)$LABORSUP(region,"HIREMAX")..
          HIRED(region)   =L= LABORSUP(region,"HIREMAX") ;
 MIXREG(CROP,SUBREG)$SUM(CRPMIXALT,MIXDATA(CROP,SUBREG,CRPMIXALT))..
           SUM((WTECH,TECH)$FARMPROD("SLIPPAGE",CROP),
                 CROPBUDGET(SUBREG,CROP,WTECH,"PARTICIP",TECH)

                *(CCCBUDDATA("CROPLAND",SUBREG,CROP,WTECH,"PARTICIP",TECH)
                 -CCCBUDDATA("ADDLAND",SUBREG,CROP,WTECH,"PARTICIP",TECH))
               + CROPBUDGET(SUBREG,CROP,WTECH,"NONPART",TECH)

                *CCCBUDDATA("CROPLAND",SUBREG,CROP,WTECH,"NONPART",TECH)  )
          +SUM((WTECH,TECH)$(FARMPROD("SLIPPAGE",CROP) LE 0.0),
                 CROPBUDGET(SUBREG,CROP,WTECH,"BASE",TECH)

                *CCCBUDDATA("CROPLAND",SUBREG,CROP,WTECH,"BASE",TECH)     )
          -SUM(CRPMIXALT
              ,MIXDATA(CROP,SUBREG,CRPMIXALT)*MIXR(SUBREG,CRPMIXALT))
          + TWID(CROP,SUBREG)
          =E= 0.0;

 MIXREGTOT(SUBREG)
         $SUM(CRPMIXALT,SUM(CROP,MIXDATA(CROP,SUBREG,CRPMIXALT)))..
           SUM((CROP,WTECH,TECH)$(FARMPROD("SLIPPAGE",CROP) GT 0. AND
                 SUM(CRPMIXALT,MIXDATA(CROP,SUBREG,CRPMIXALT)) GT 0.),
                 CROPBUDGET(SUBREG,CROP,WTECH,"PARTICIP",TECH)

                *(CCCBUDDATA("CROPLAND",SUBREG,CROP,WTECH,"PARTICIP",TECH)
                 -CCCBUDDATA("ADDLAND",SUBREG,CROP,WTECH,"PARTICIP",TECH))
               +  CROPBUDGET(SUBREG,CROP,WTECH,"NONPART",TECH)

                *CCCBUDDATA("CROPLAND",SUBREG,CROP,WTECH,"NONPART",TECH)  )
           +SUM((CROP,WTECH,TECH)$(FARMPROD("SLIPPAGE",CROP) LE 0.0 AND
                 SUM(CRPMIXALT,MIXDATA(CROP,SUBREG,CRPMIXALT)) GT 0.),
                 CROPBUDGET(SUBREG,CROP,WTECH,"BASE",TECH)

                *CCCBUDDATA("CROPLAND",SUBREG,CROP,WTECH,"BASE",TECH)     )
         - SUM(CRPMIXALT,SUM(CROP,
               MIXDATA(CROP,SUBREG,CRPMIXALT))*MIXR(SUBREG,CRPMIXALT))
         =E= 0.;


 MIXNAT(PRIMARY,SUBREG)
            $SUM(NATMIXALT,NATMIXDATA(SUBREG,PRIMARY,NATMIXALT))..
            SUM((CROP,WTECH,TECH)
               $(FARMPROD("SLIPPAGE",CROP)
                *CCCBUDDATA(PRIMARY,SUBREG,CROP,WTECH,"PARTICIP",TECH)),
                 CROPBUDGET(SUBREG,CROP,WTECH,"PARTICIP",TECH)

              +   CROPBUDGET(SUBREG,CROP,WTECH,"NONPART",TECH)

                *CCCBUDDATA(PRIMARY,SUBREG,CROP,WTECH,"NONPART",TECH)  )
           +SUM((CROP,WTECH,TECH)$(FARMPROD("SLIPPAGE",CROP) LE 0.0),
                 CROPBUDGET(SUBREG,CROP,WTECH,"BASE",TECH)

                *CCCBUDDATA(PRIMARY,SUBREG,CROP,WTECH,"BASE",TECH)     )
           +SUM((ANIMAL,LIVETECH)$(LBUDDATA(PRIMARY,SUBREG,ANIMAL,LIVETECH) gt 0),
               LVSTBUDGET(SUBREG,ANIMAL,LIVETECH)

               *LBUDDATA(PRIMARY,SUBREG,ANIMAL,LIVETECH))
         - SUM(NATMIXALT,
               NATMIXDATA(SUBREG,PRIMARY,NATMIXALT)*NATMIX(PRIMARY,NATMIXALT))
         + TOLR(PRIMARY,SUBREG) =E= 0.;


*>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
*BOUNDS
*>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
*set in asmcalrn.94

*>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>

* INITIAL VALUES FOR NONLINEAR VARIABLES

 IMPORTP.L(PRIMARY)$(PIMPORT(PRIMARY,"QUANTITY") GT 0 OR
                      PIMPORT(PRIMARY,"ELASTICITY") GT 0)
              = PIMPORT(PRIMARY,"QUANTITY")
                      ;
 EXPORTP.L(PRIMARY)$(PEXPORT(PRIMARY,"QUANTITY") GT 0 OR
                      PEXPORT(PRIMARY,"ELASTICITY") GT 0)
              = PEXPORT(PRIMARY,"QUANTITY")
                      ;
 DEMANDP.L(PRIMARY)$(PDEMAND(PRIMARY,"QUANTITY") GT 0 OR
                      PDEMAND(PRIMARY,"ELASTICITY") GT 0)
              = PDEMAND(PRIMARY,"QUANTITY")
                      ;
 IMPORTS.L(SECONDARY)$(SIMPORT(SECONDARY,"QUANTITY") GT 0 OR
                      SIMPORT(SECONDARY,"ELASTICITY") GT 0)
              = SIMPORT(SECONDARY,"QUANTITY")
                ;
 EXPORTS.L(SECONDARY)$(SEXPORT(SECONDARY,"QUANTITY") GT 0 OR
                      SEXPORT(SECONDARY,"ELASTICITY") GT 0)
              = SEXPORT(SECONDARY,"QUANTITY")
                ;
 DEMANDS.L(SECONDARY)$(SDEMAND(SECONDARY,"QUANTITY") GT 0 OR
                      SDEMAND(SECONDARY,"ELASTICITY") GT 0)
              = SDEMAND(SECONDARY,"QUANTITY")
                ;
 LANDSUPPLY.L(SUBREG,LANDTYPE)$LANDSUPPL(LANDTYPE,SUBREG,"ELASTICITY")
              = LANDSUPPL(LANDTYPE,SUBREG,"QUANTITY")
              ;
 AUMSPRIV.L(SUBREG)$AUMSSUP(SUBREG,"PRIVATELAS")
              = AUMSSUP(SUBREG,"PRIVATEQ") ;
 WATERVAR.L(SUBREG)$WATERSUP(SUBREG,"PUMPELAS")
              = WATERSUP(SUBREG,"PUMPQ") ;
 HIRED.L(REGION) $LABORSUP(REGION,"HIREELAS")
              = LABORSUP(REGION,"HIREQ") ;

$include asmscale10.gms
$include asmcalrn.gms
scalar lim  limit on farm program iterations ;
lim=card(iter);
lim=1         ;
MODEL SECTOR /ALL/;
sector.scaleopt=1;
option CCcbuddata:3:5:1;
display CCcbuddata;
option solveopt=replace;
OPTION SOLPRINT = off ;
option limrow = 0
option limcol = 0
option iterlim = 500000;
OPTION RESLIM = 200000;
*$INCLUDE basis.bas
$INCLUDE asmdef
TOL = 0.00015;
option lp=cplex;
$ontext
#user model library stuff
Main topic ASM Model
Featured item 1 Code seperation
Featured item 2 .. specification
Featured item 3 ASM sector model
Description
Defines model equations for ASM model
$offtext
