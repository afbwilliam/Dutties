*Illustrates of small model structure development and expandability
*in contrast with transml.gms

SETS  PLANT    PLANT LOCATIONS
             /NEWYORK  , CHICAGO , LOSANGLS , BALTIMORE , WASHINGTON
              PHILADEL , LASVEGAS,  RENO    , SEATTLE   , BOISE/
      MARKET   DEMAND MARKETS
                /MIAMI,   HOUSTON, MINEPLIS, PORTLAND,BOSTON/
PARAMETERS   SUPPLY(PLANT)  QUANTITY AVAILABLE AT EACH PLANT
                /NEWYORK   100, CHICAGO    75, LOSANGLS   90,
                 BALTIMORE  80, WASHINGTON 70, PHILADEL   60,
                 LASVEGAS   40, RENO       20, SEATTLE    55,
                 BOISE      10/
             DEMAND(MARKET)   QUANTITY REQUIRED BY DEMAND MARKET
                /MIAMI     100, HOUSTON    90,
                 MINEPLIS  120, PORTLAND   90
                 BOSTON    180/;
 TABLE DISTANCE(PLANT,MARKET) DISTANCE FROM EACH PLANT TO EACH MARKET
                     MIAMI   HOUSTON   MINEPLIS   PORTLAND  BOSTON
         NEWYORK      1300     1800       1100       3600     150
         CHICAGO      2200     1300        700       2900     800
         LOSANGLS     3700     2400       2500       1100    3800
         BALTIMORE    1100     1600       1200       3700     350
         WASHINGTON    1050     1550       1200       3700     400
         PHILADEL     1200     1700       1150       3650     250
         LASVEGAS     3300     2100       2300       1300    3600
         RENO         3400     2200       2200        900    3400
         SEATTLE       3700     2500       1900        250    3500
         BOISE        3500     2200       1700        450    3300      ;
 PARAMETER COST(PLANT,MARKET)    CALCULATED COST OF MOVING GOODS;
           COST(PLANT,MARKET) = 50 + 1 * DISTANCE(PLANT,MARKET);
 POSITIVE VARIABLES
         SHIPMENTS(PLANT,MARKET) AMOUNT SHIPPED OVER A TRANSPORT ROUTE;
 VARIABLES TCOST                 TOTAL COST OF SHIPPING OVER ALL ROUTES;
 EQUATIONS TCOSTEQ               TOTAL COST ACCOUNTING EQUATION
           SUPPLYEQ(PLANT)       LIMIT ON SUPPLY AVAILABLE AT A PLANT
           DEMANDEQ(MARKET)      MINIMUM REQUIREMENT AT A DEMAND MARKET;
 TCOSTEQ.. TCOST =E=SUM((PLANT,MARKET), SHIPMENTS(PLANT,MARKET)*
                                               COST(PLANT,MARKET));
 SUPPLYEQ(PLANT).. SUM(MARKET,SHIPMENTS(PLANT,MARKET))=L=SUPPLY(PLANT);
 DEMANDEQ(MARKET)..SUM(PLANT,SHIPMENTS(PLANT,MARKET))=G=DEMAND(MARKET);
 MODEL TRANSPORT /ALL/;
 SOLVE TRANSPORT USING LP MINIMIZING TCOST;
PARAMETER MOVEMENT(*,*)  COMMODITY MOVEMENT;
MOVEMENT(PLANT,MARKET)=SHIPMENTS.L(PLANT,MARKET);
MOVEMENT("TOTAL",MARKET)=SUM(PLANT,SHIPMENTS.L(PLANT,MARKET));
MOVEMENT(PLANT,"TOTAL")=SUM(MARKET,SHIPMENTS.L(PLANT,MARKET));
MOVEMENT("TOTAL","TOTAL")=SUM(MARKET,MOVEMENT("TOTAL",MARKET));
OPTION DECIMALS=0;
DISPLAY MOVEMENT;
PARAMETER COSTS(*,*)  COMMODITY MOVEMENT COSTS BY ROUTE;
COSTS(PLANT,MARKET)=COST(PLANT,MARKET)*SHIPMENTS.L(PLANT,MARKET);
COSTS("TOTAL",MARKET)
    =SUM(PLANT,COST(PLANT,MARKET)*SHIPMENTS.L(PLANT,MARKET));
COSTS(PLANT,"TOTAL")
    =SUM(MARKET,COST(PLANT,MARKET)*SHIPMENTS.L(PLANT,MARKET));
COSTS("TOTAL","TOTAL")=TCOST.L;
OPTION DECIMALS=0;
DISPLAY COSTS;
PARAMETER SUPPLYREP(PLANT,*)  SUPPLY REPORT;
SUPPLYREP(PLANT,"AVAILABLE")=SUPPLY(PLANT);
SUPPLYREP(PLANT,"USED")=MOVEMENT(PLANT,"TOTAL");
SUPPLYREP(PLANT,"MARGVALUE")=ABS(SUPPLYEQ.M(PLANT));
OPTION DECIMALS=2;
DISPLAY SUPPLYREP;
PARAMETER DEMANDREP(MARKET,*)  DEMAND REPORT;
DEMANDREP(MARKET,"REQUIRED")=DEMAND(MARKET);
DEMANDREP(MARKET,"RECIEVED")=MOVEMENT("TOTAL",MARKET);
DEMANDREP(MARKET,"MARGCOST")=ABS(DEMANDEQ.M(MARKET));
OPTION DECIMALS=2;
DISPLAY DEMANDREP;
PARAMETER CMOVEMENT(*,*) COSTS OF CHANGING COMMODITY MOVEMENT PATTERN;
CMOVEMENT(PLANT,MARKET)=SHIPMENTS.M(PLANT,MARKET);
OPTION DECIMALS=2;
DISPLAY CMOVEMENT;

$ontext
#user model library stuff
Main topic Small to large
Featured item 1 Save restart
Featured item 2 Small model
Featured item 3
Featured item 4
include transml.gms
Description
Illustrates of small model structure development and expandability
in contrast with transml.gms

$offtext
