$ontext

Report writer for ASM model

$offtext

 BALANCEP(PRIMARY,BALITEM)    = 0;
 BALANCES(SECONDARY,BALITEM)  = 0;
 GOVDEF(ALLI,ITEMDEF)         = 0;
 GOVCCC(ALLI,ITEMCCC)         = 0;
 WELSUM(WELITEM)              = 0;
 GROSSREV(ALLI,GROSSITEM)     = 0;

*##################################################
*   WRITING   ASM   REPORTS
*##################################################

 NONFEED(PROCESSALT) = YES; NONFEED(MIXFEED)=NO;

BALANCEP(PRIMARY,"PRODUCTION") =
  SUM(SUBREG,
    SUM((CROP,WTECH,TECH)$FARMPROD("SLIPPAGE",CROP),
      CROPBUDGET.L(SUBREG,CROP,WTECH,"NONPART",TECH) *SCALPROD
        *(CCCBUDDATA(PRIMARY,SUBREG,CROP,WTECH,"NONPART",TECH))$
         (CCCBUDDATA(PRIMARY,SUBREG,CROP,WTECH,"NONPART",TECH) GT 0)

    + CROPBUDGET.L(SUBREG,CROP,WTECH,"PARTICIP",TECH)  *SCALPROD
        *CCCBUDDATA(PRIMARY,SUBREG,CROP,WTECH,"NONPART",TECH)
        *(1.0-FPPART(SUBREG,CROP))$
         (CCCBUDDATA(PRIMARY,SUBREG,CROP,WTECH,"NONPART",TECH) GT 0)

    + CROPBUDGET.L(SUBREG,CROP,WTECH,"PARTICIP",TECH)
        *SCALPROD
        *CCCBUDDATA(PRIMARY,SUBREG,CROP,WTECH,"PARTICIP",TECH)
        *FPPART(SUBREG,CROP)
        * (1-FARMPROD("PERCNTPAID",CROP))$
          (CCCBUDDATA(PRIMARY,SUBREG,CROP,WTECH,"PARTICIP",TECH) GT 0)

    + CROPBUDGET.L(SUBREG,CROP,WTECH,"PARTICIP",TECH)
        *SCALPROD
        *CCCBUDDATA(PRIMARY,SUBREG,CROP,WTECH,"PARTICIP",TECH)
        *FPPART(SUBREG,CROP)*FARMPROD("FLEX",CROP)
        * (FARMPROD("PERCNTPAID",CROP))$
         (CCCBUDDATA(PRIMARY,SUBREG,CROP,WTECH,"PARTICIP",TECH) GT 0))

+ SUM((CROP,WTECH,TECH)$(FARMPROD("SLIPPAGE",CROP) GT 0.0 AND
                         FARMPROD("FPYIELD",CROP) LE 1.0),
      CROPBUDGET.L(SUBREG,CROP,WTECH,"PARTICIP",TECH)
        *SCALPROD
        *FPPART(SUBREG,CROP)
        * FARMPROD("PERCNTPAID",CROP)
        *CCCBUDDATA(PRIMARY,SUBREG,CROP,WTECH,"PARTICIP",TECH)
        *(1.0-FARMPROD("FPYIELD",CROP))$
         (CCCBUDDATA(PRIMARY,SUBREG,CROP,WTECH,"PARTICIP",TECH) GT 0) )

+ SUM((CROP,WTECH,TECH)$(FARMPROD("SLIPPAGE",CROP) LE 0.),
      CROPBUDGET.L(SUBREG,CROP,WTECH,"BASE",TECH)
        *SCALPROD
        *(CCCBUDDATA(PRIMARY,SUBREG,CROP,WTECH,"BASE",TECH))$
          (CCCBUDDATA(PRIMARY,SUBREG,CROP,WTECH,"BASE",TECH) GT 0)    )

+ SUM((ANIMAL,LIVETECH),
      LVSTBUDGET.L(SUBREG,ANIMAL,LIVETECH)
        *SCALLIVE
        *LBUDDATA(PRIMARY,SUBREG,ANIMAL,LIVETECH)$
         (LBUDDATA(PRIMARY,SUBREG,ANIMAL,LIVETECH) GT 0)   )   )

+ DEFPRODN.L(PRIMARY)$FARMPROD("SLIPPAGE",PRIMARY)*SCALE(PRIMARY)

      ;


 BALANCEP(PRIMARY,"IMPORT") =  IMPORTP.L(PRIMARY) *SCALE(PRIMARY) ;


 BALANCEP(PRIMARY,"PROD-USE") =
   -SUM(SUBREG,
      SUM((CROP,WTECH,CTECH,TECH),
         CROPBUDGET.L(SUBREG,CROP,WTECH,CTECH,TECH)
          *SCALPROD
          *(CCCBUDDATA(PRIMARY,SUBREG,CROP,WTECH,CTECH,TECH))$
             (CCCBUDDATA(PRIMARY,SUBREG,CROP,WTECH,CTECH,TECH) LT 0))

    +SUM((ANIMAL,LIVETECH),
         LVSTBUDGET.L(SUBREG,ANIMAL,LIVETECH)
          *SCALLIVE
          *LBUDDATA(PRIMARY,SUBREG,ANIMAL,LIVETECH)$
           (LBUDDATA(PRIMARY,SUBREG,ANIMAL,LIVETECH) LT 0) )) ;


 BALANCEP(PRIMARY,"FEEDMIXUSE") =
        SUM(MIXFEED,
            PROCESS.L(MIXFEED)*SCALPROC*PROCBUD(PRIMARY,MIXFEED)) ;


 BALANCEP(PRIMARY,"PROC-USE") =
        +SUM(NONFEED$(PROCBUD(PRIMARY,NONFEED) GT 0),
             PROCESS.L(NONFEED)*SCALPROC*PROCBUD(PRIMARY,NONFEED)) ;


 BALANCEP(PRIMARY,"DOM-DEMAND") =   DEMANDP.L(PRIMARY)*SCALE(PRIMARY) ;


 BALANCEP(PRIMARY,"EXPORT") = EXPORTP.L(PRIMARY)*SCALE(PRIMARY);


 BALANCEP(PRIMARY,"PRICEX100")
    =  PRIMARYBAL.M(PRIMARY) * 100. /SCALE(PRIMARY)*SCALOBJ;


 BALANCEP(PRIMARY,"CCCLOANSTK") =  CCCLOANP.L(PRIMARY)*SCALE(PRIMARY);


 OPTION DECIMALS = 0 ;


*######################################################################

 BALANCES(SECONDARY,"PROC-YLD") =
      +SUM(PROCESSALT,
           PROCESS.L(PROCESSALT)*PROCBUD(SECONDARY,PROCESSALT)
           *SCALPROC$(PROCBUD(SECONDARY,PROCESSALT) GT 0))  ;


 BALANCES(SECONDARY,"IMPORT") =  IMPORTS.L(SECONDARY)*SCALE(SECONDARY) ;


 BALANCES(SECONDARY,"PROD-USE") =
      +SUM(SUBREG,
         SUM((CROP,WTECH,CTECH,TECH)$FARMPROD("SLIPPAGE",CROP),
            CROPBUDGET.L(SUBREG,CROP,WTECH,"PARTICIP",TECH)
            *SCALPROD
            *CCCBUDDATA(secondary,SUBREG,CROP,WTECH,"PARTICIP",TECH)

           +CROPBUDGET.L(SUBREG,CROP,WTECH,"NONPART",TECH)
            *SCALPROD
            *CCCBUDDATA(secondary,SUBREG,CROP,WTECH,"NONPART",TECH)  )
        +SUM((CROP,WTECH,CTECH,TECH)$(FARMPROD("SLIPPAGE",CROP) LE 0.0),
            CROPBUDGET.L(SUBREG,CROP,WTECH,"BASE",TECH)
            *SCALPROD
            *CCCBUDDATA(secondary,SUBREG,CROP,WTECH,"BASE",TECH)     )

        +SUM((ANIMAL,LIVETECH),
            LVSTBUDGET.L(SUBREG,ANIMAL,LIVETECH)
            *SCALLIVE
            *LBUDDATA(secondary,SUBREG,ANIMAL,LIVETECH)      )   );


 BALANCES(SECONDARY,"FEEDMIXUSE") =
       -SUM(MIXFEED,
            PROCESS.L(MIXFEED)*SCALPROC*PROCBUD(SECONDARY,MIXFEED)
            $(PROCBUD(SECONDARY,MIXFEED) LT 0) ) ;


 BALANCES(SECONDARY,"PROC-USE") =
       -SUM(NONFEED,
            PROCESS.L(NONFEED)*SCALPROC*PROCBUD(SECONDARY,NONFEED)
            $(PROCBUD(SECONDARY,NONFEED) LT 0) ) ;


 BALANCES(SECONDARY,"DOM-DEMAND") = DEMANDS.L(SECONDARY)*SCALE(SECONDARY) ;

 BALANCES(SECONDARY,"EXPORT")     = EXPORTS.L(SECONDARY)*SCALE(SECONDARY) ;

 BALANCES(SECONDARY,"PRICEX100")  =
          SECONDBAL.M(SECONDARY) * 100./SCALE(SECONDARY)*SCALOBJ ;


 BALANCES(SECONDARY,"CCCLOANSTK")     =
          CCCLOANS.L(SECONDARY) *SCALE(SECONDARY) ;


 OPTION DECIMALS = 0 ;


*######################################################################


  GOVDEF(PRIMARY,"DEFRATE")$(FARMPROD("TARGET",PRIMARY) GT 0)
      = MAX(0.0, (FARMPROD("TARGET",PRIMARY)-
           MAX(FARMPROD("LOANRATE",PRIMARY),PRIMARYBAL.M(PRIMARY)
          /SCALE(PRIMARY)*SCALOBJ) )     ) ;


  GOVDEF(PRIMARY,"DEFPYMT")$FARMPROD("TARGET",PRIMARY)
       =GOVDEF(PRIMARY,"DEFRATE") * DEFPRODN.L(PRIMARY)*SCALE(PRIMARY);

  GOVDEF(PRIMARY,"5092PYMT")$(FARMPROD("TARGET",PRIMARY)
      *FARMPROD("50-92",PRIMARY))
       =GOVDEF(PRIMARY,"DEFRATE") * PRDN5092.L(PRIMARY)*SCALE(PRIMARY) *0.42;

  GOVDEF(PRIMARY,"092pymt")$(FARMPROD("TARGET",PRIMARY)
      *FARMPROD("0-92",PRIMARY))
       =GOVDEF(PRIMARY,"DEFRATE") * PRDN092.L(PRIMARY)*SCALE(PRIMARY) *0.92;

  GOVDEF(PRIMARY,"DIVPYMT")$(FARMPROD("TARGET",PRIMARY)
      *FARMPROD("diversion",PRIMARY))
       =FARMPROD("DIVERPAY",PRIMARY) * DIVPRODN.L(PRIMARY)*SCALE(PRIMARY);

  GOVDEF(PRIMARY,"UNHARVPYMT")$(FARMPROD("TARGET",PRIMARY)
      *FARMPROD("unharvacr",PRIMARY))
       =GOVDEF(PRIMARY,"DEFRATE") * UNHARV.L(PRIMARY)*SCALE(PRIMARY);

  GOVDEF(PRIMARY,"ARTIFPYMT")$FARMPROD("TARGET",PRIMARY)
       =GOVDEF(PRIMARY,"DEFRATE") * ARTIF.L(PRIMARY)*SCALE(PRIMARY);

  GOVDEF(PRIMARY,"TOTDEFPYMT")$FARMPROD("TARGET",PRIMARY)
       = GOVDEF(PRIMARY,"DEFPYMT") + GOVDEF(PRIMARY,"5092PYMT")
        +GOVDEF(PRIMARY,"DIVPYMT") +GOVDEF(PRIMARY,"UNHARVPYMT")
        +GOVDEF(PRIMARY,"ARTIFPYMT")   + GOVDEF(PRIMARY,"092pymt");

  GOVDEF(PRIMARY,"MKTRATE")$(FARMPROD("MKTLOANY-N",PRIMARY) GT 0
           AND FARMPROD("LOANRATE",PRIMARY) GT PRIMARYBAL.M(PRIMARY)
             /SCALE(PRIMARY)*SCALOBJ )
        =FARMPROD("LOANRATE",PRIMARY) - PRIMARYBAL.M(PRIMARY)
                       /SCALE(PRIMARY)*SCALOBJ ;

  GOVDEF(PRIMARY,"MKTPYMT")$(FARMPROD("MKTLOANY-N",PRIMARY) GT 0
           AND FARMPROD("LOANRATE",PRIMARY) GT PRIMARYBAL.M(PRIMARY)
                    /SCALE(PRIMARY)*SCALOBJ )
        = GOVDEF(PRIMARY,"MKTRATE") * DEFPRODN.L(PRIMARY)*SCALE(PRIMARY) ;

  GOVDEF(PRIMARY,"DEF+MKT")$(FARMPROD("TARGET",PRIMARY) GT 0 OR
                   FARMPROD("MKTLOANY-N",PRIMARY) GT 0 )
       = GOVDEF(PRIMARY,"TOTDEFPYMT") + GOVDEF(PRIMARY,"MKTPYMT") ;

  GOVDEF("TOTAL", IDEF)
       = SUM(PRIMARY, GOVDEF(PRIMARY,IDEF)) ;

  GOVCCC(PRIMARY,"CCCSTK")$(FARMPROD("LOANRATE",PRIMARY) GT 0 AND
                   FARMPROD("MKTLOANY-N",PRIMARY) LE 0 )
       = CCCLOANP.L(PRIMARY)*SCALE(PRIMARY) ;

  GOVCCC(PRIMARY,"CCCRATE")$(FARMPROD("LOANRATE",PRIMARY) GT 0 AND
                   FARMPROD("MKTLOANY-N",PRIMARY) LE 0 )
       = FARMPROD("LOANRATE",PRIMARY);

  GOVCCC(PRIMARY,"CCCLOANCST")$(FARMPROD("LOANRATE",PRIMARY) GT 0 AND
                   FARMPROD("MKTLOANY-N",PRIMARY) LE 0 )
       = GOVCCC(PRIMARY,"CCCRATE") * GOVCCC(PRIMARY,"CCCSTK") ;

  GOVCCC(SECONDARY,"CCCSTK")$(FARMPROD("LOANRATE",SECONDARY) GT 0 AND
                   FARMPROD("MKTLOANY-N",SECONDARY) LE 0 )
       = CCCLOANS.L(SECONDARY)*SCALE(SECONDARY) ;

  GOVCCC(SECONDARY,"CCCRATE")$(FARMPROD("LOANRATE",SECONDARY) GT 0 AND
                   FARMPROD("MKTLOANY-N",SECONDARY) LE 0 )
       = FARMPROD("LOANRATE",SECONDARY);

  GOVCCC(SECONDARY,"CCCLOANCST")$(FARMPROD("LOANRATE",SECONDARY) GT 0 AND
                   FARMPROD("MKTLOANY-N",SECONDARY) LE 0 )
       = GOVCCC(SECONDARY,"CCCRATE") * GOVCCC(SECONDARY,"CCCSTK") ;

  GOVCCC("TOTAL",ICCC)
       = SUM(PRIMARY,GOVCCC(PRIMARY,ICCC))
         + SUM(SECONDARY,GOVCCC(SECONDARY,ICCC)) ;


OPTION DECIMALS=3;

  WELSUM("GOV-DEFPMT")= GOVDEF("TOTAL","TOTDEFPYMT");
  WELSUM("GOV-MKTLN")= GOVDEF("TOTAL","MKTPYMT");
  WELSUM("GOV-TOTPMT")=GOVDEF("TOTAL","TOTDEFPYMT")+GOVDEF("TOTAL","MKTPYMT");

*&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
*    GROSS REVENUE REPORT BY COMMODITY
*&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

GROSSREV(PRIMARY,"PRODUCTION")=  BALANCEP(PRIMARY,"PRODUCTION") ;
GROSSREV(PRIMARY,"PRICE")=  BALANCEP(PRIMARY,"PRICEX100") /100.;
GROSSREV(PRIMARY,"DEFPYMT")=   GOVDEF(PRIMARY,"DEF+MKT") ;
GROSSREV(PRIMARY,"TOTAL-REV")= GROSSREV(PRIMARY,"PRODUCTION")*
            GROSSREV(PRIMARY,"PRICE") + GROSSREV(PRIMARY,"DEFPYMT");

GROSSREV(SECONDARY,"PRODUCTION")=  BALANCES(SECONDARY,"PROC-YLD") ;
GROSSREV(SECONDARY,"PRICE")=  BALANCES(SECONDARY,"PRICEX100") /100.;
GROSSREV(SECONDARY,"TOTAL-REV")= GROSSREV(SECONDARY,"PRODUCTION")*
            GROSSREV(SECONDARY,"PRICE") ;




*&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
*    SUBREG WELFARE REPORT
*&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

DOMCSP(PRIMARY) =

$if not "%solveasm%" == "nonlinear"      $goto linear11
$if     "%solveasm%" == "nonlinear"      $goto nlinear11

$label nlinear11
   ((DEMANDP.L(PRIMARY)/PDEMAND(PRIMARY,"QUANTITY"))
                      **(1./PDEMAND(PRIMARY,"ELASTICITY"))
   * DEMANDP.L(PRIMARY) * PDEMAND(PRIMARY,"PRICE")
   * PDEMAND(PRIMARY,"ELASTICITY")  / (1.+PDEMAND(PRIMARY,"ELASTICITY"))
   + PDEMAND(PRIMARY,"CONSTANT1")+PDEMAND(PRIMARY,"CONSTANT2"))
  $(PDEMAND(PRIMARY,"QUANTITY")   GT 0 AND
               PDEMAND(PRIMARY,"PRICE")      GT 0 AND
               PDEMAND(PRIMARY,"ELASTICITY") LT -0.05     )
$goto continue11

$label linear11
  +(SUM(STEPS$(PDEMAND(PRIMARY,"TFAC") GE 1/QINC(STEPS)),
       (QINC(STEPS) **(1./PDEMAND(PRIMARY,"ELASTICITY"))
       * PDEMAND(PRIMARY,"QUANTITY")
       * QINC(STEPS)
       * PDEMAND(PRIMARY,"PRICE")
       * PDEMAND(PRIMARY,"ELASTICITY")
       / (1.+PDEMAND(PRIMARY,"ELASTICITY"))
       + PDEMAND(PRIMARY,"CONSTANT1")
       + PDEMAND(PRIMARY,"CONSTANT2")
       )*DEMANDPS.L(PRIMARY,STEPS))
    )$(
       PDEMAND(PRIMARY,"QUANTITY")   GT 0 AND
       PDEMAND(PRIMARY,"PRICE")      GT 0 AND
       PDEMAND(PRIMARY,"ELASTICITY") LT -0.05     )
$goto continue11

$label continue11
 + (PDEMAND(PRIMARY,"PRICE")*DEMANDP.L(PRIMARY))
      $(PDEMAND(PRIMARY,"PRICE")      GT 0 AND
        PDEMAND(PRIMARY,"ELASTICITY") GE -0.05
        AND PDEMAND(PRIMARY,"QUANTITY") NE 0   )

- PRIMARYBAL.M(PRIMARY)*DEMANDP.L(PRIMARY)
;

EXPORCSP(PRIMARY)=


$if not "%solveasm%" == "nonlinear"      $goto linear21
$if     "%solveasm%" == "nonlinear"      $goto nlinear21

$label nlinear21
   ((EXPORTP.L(PRIMARY)/PEXPORT(PRIMARY,"QUANTITY"))
                      **(1./PEXPORT(PRIMARY,"ELASTICITY"))
   * EXPORTP.L(PRIMARY) * PEXPORT(PRIMARY,"PRICE")
   * PEXPORT(PRIMARY,"ELASTICITY") / (1.+PEXPORT(PRIMARY,"ELASTICITY"))
   +PEXPORT(PRIMARY,"CONSTANT1")+PEXPORT(PRIMARY,"CONSTANT2")  )
   $(PEXPORT(PRIMARY,"QUANTITY")   GT 0 AND
             PEXPORT(PRIMARY,"PRICE")      GT 0 AND
             PEXPORT(PRIMARY,"ELASTICITY") LT -0.05     )
$goto continue21

$label linear21
   + (SUM(STEPS$(PEXPORT(PRIMARY,"TFAC") GE 1/QINC(STEPS)),
         (QINC(STEPS) **(1./PEXPORT(PRIMARY,"ELASTICITY"))
         * PEXPORT(PRIMARY,"QUANTITY")
         * QINC(STEPS)
         * PEXPORT(PRIMARY,"PRICE")
         * PEXPORT(PRIMARY,"ELASTICITY")
         / (1.+PEXPORT(PRIMARY,"ELASTICITY"))
         + PEXPORT(PRIMARY,"CONSTANT1")
         + PEXPORT(PRIMARY,"CONSTANT2"))
      * EXPORTPS.l(PRIMARY,STEPS)
         )   )$(
                PEXPORT(PRIMARY,"QUANTITY")   GT 0 AND
                PEXPORT(PRIMARY,"PRICE")      GT 0 AND
                PEXPORT(PRIMARY,"ELASTICITY") LT -0.05     )
$goto continue21

$label continue21
   + (PEXPORT(PRIMARY,"PRICE")*EXPORTP.l(PRIMARY) )
        $(PEXPORT(PRIMARY,"PRICE")      GT 0 AND
          PEXPORT(PRIMARY,"ELASTICITY") GE -0.05
          AND PEXPORT(PRIMARY,"QUANTITY") NE 0    )

- PRIMARYBAL.M(PRIMARY)* EXPORTP.L(PRIMARY)

 ;


IMPORPSP(PRIMARY)=


$if not "%solveasm%" == "nonlinear"      $goto linear31
$if     "%solveasm%" == "nonlinear"      $goto nlinear31

$label nlinear31
  - PIMPORT(PRIMARY,"ELASTICITY") /(1.+PIMPORT(PRIMARY,"ELASTICITY"))
  *(PIMPORT(PRIMARY,"PRICE")
  *(
((IMPORTP.L(PRIMARY)/PIMPORT(PRIMARY,"QUANTITY"))**
                  (1./PIMPORT(PRIMARY,"ELASTICITY"))
               * IMPORTP.L(PRIMARY)) ))
 $(PIMPORT(PRIMARY,"PRICE")      GT 0 AND
              PIMPORT(PRIMARY,"QUANTITY")   GT 0 AND
             PIMPORT(PRIMARY,"ELASTICITY") GT 0.05     )
$goto continue31

$label linear31
  - PIMPORT(PRIMARY,"ELASTICITY") /(1.+PIMPORT(PRIMARY,"ELASTICITY"))
  *(PIMPORT(PRIMARY,"PRICE")
  *(
      +SUM(STEPS,(QINC(STEPS))** (1./PIMPORT(PRIMARY,"ELASTICITY"))
               * QINC(STEPS)*PIMPORT(PRIMARY,"QUANTITY")
              *IMPORTPS.L(PRIMARY,STEPS)
          )))
 $(PIMPORT(PRIMARY,"PRICE")      GT 0 AND
              PIMPORT(PRIMARY,"QUANTITY")   GT 0 AND
             PIMPORT(PRIMARY,"ELASTICITY") GT 0.05     )
$goto continue31

$label continue31
 -  (PIMPORT(PRIMARY,"PRICE")*IMPORTP.L(PRIMARY))
   $(PIMPORT(PRIMARY,"PRICE")      GT 0 AND
               PIMPORT(PRIMARY,"ELASTICITY") LE 0.05
              AND PIMPORT(PRIMARY,"QUANTITY") NE 0      )

+ IMPORTP.L(PRIMARY)* PRIMARYBAL.M(PRIMARY)
;


DOMCSS(SECONDARY)=


$if not "%solveasm%" == "nonlinear"      $goto linear41
$if     "%solveasm%" == "nonlinear"      $goto nlinear41

$label nlinear41
  ( (DEMANDS.L(SECONDARY)/SDEMAND(SECONDARY,"QUANTITY"))
                      **(1./SDEMAND(SECONDARY,"ELASTICITY"))
  * DEMANDS.L(SECONDARY)   * SDEMAND(SECONDARY,"PRICE")
  * SDEMAND(SECONDARY,"ELASTICITY")   / (1.+SDEMAND(SECONDARY,"ELASTICITY"))
  +SDEMAND(SECONDARY,"CONSTANT1")+SDEMAND(SECONDARY,"CONSTANT2"))
 $(SDEMAND(SECONDARY,"QUANTITY")   GT 0 AND
             SDEMAND(SECONDARY,"PRICE")      GT 0 AND
             SDEMAND(SECONDARY,"ELASTICITY") LT -0.05     )

$goto continue41

$label linear41
+ (SUM(STEPS$(SDEMAND(SECONDARY,"TFAC") GE 1/QINC(STEPS)),
      (QINC(STEPS) **(1./SDEMAND(SECONDARY,"ELASTICITY"))
       * SDEMAND(SECONDARY,"QUANTITY")
       * QINC(STEPS)
       * SDEMAND(SECONDARY,"PRICE")
       * SDEMAND(SECONDARY,"ELASTICITY")
       / (1.+SDEMAND(SECONDARY,"ELASTICITY"))
       + SDEMAND(SECONDARY,"CONSTANT1")
       + SDEMAND(SECONDARY,"CONSTANT2")
      )*DEMANDSS.L(SECONDARY,STEPS))
  )$(
        SDEMAND(SECONDARY,"QUANTITY")   GT 0 AND
        SDEMAND(SECONDARY,"PRICE")      GT 0 AND
        SDEMAND(SECONDARY,"ELASTICITY") LT -0.05     )
$goto continue41


$label continue41
+ (SDEMAND(SECONDARY,"PRICE")*DEMANDS.L(SECONDARY))
      $(SDEMAND(SECONDARY,"PRICE")      GT 0 AND
        SDEMAND(SECONDARY,"ELASTICITY") GE -0.05
        AND SDEMAND(SECONDARY,"QUANTITY") NE 0   )

- SECONDBAL.M(SECONDARY)*DEMANDS.L(SECONDARY)
;


EXPORCSS(SECONDARY)=


$if not "%solveasm%" == "nonlinear"      $goto linear51
$if     "%solveasm%" == "nonlinear"      $goto nlinear51

$label nlinear51
   ((EXPORTS.L(SECONDARY)/SEXPORT(SECONDARY,"QUANTITY"))
                      **(1./SEXPORT(SECONDARY,"ELASTICITY"))
   * EXPORTS.L(SECONDARY)   * SEXPORT(SECONDARY,"PRICE")
   * SEXPORT(SECONDARY,"ELASTICITY") / (1.+SEXPORT(SECONDARY,"ELASTICITY"))
   +SEXPORT(SECONDARY,"CONSTANT1") +SEXPORT(SECONDARY,"CONSTANT2")  )
  $(SEXPORT(SECONDARY,"QUANTITY")   GT 0 AND
                SEXPORT(SECONDARY,"PRICE")      GT 0 AND
                SEXPORT(SECONDARY,"ELASTICITY") LT -0.05     )

$goto continue51

$label linear51
  + (SUM(STEPS$(SEXPORT(SECONDARY,"TFAC") GE 1/QINC(STEPS)),
        (QINC(STEPS) **(1./SEXPORT(SECONDARY,"ELASTICITY"))
        * SEXPORT(SECONDARY,"QUANTITY")
        * QINC(STEPS)
        * SEXPORT(SECONDARY,"PRICE")
        * SEXPORT(SECONDARY,"ELASTICITY")
        / (1.+SEXPORT(SECONDARY,"ELASTICITY"))
        + SEXPORT(SECONDARY,"CONSTANT1")
        + SEXPORT(SECONDARY,"CONSTANT2"))
       *EXPORTSS.L(SECONDARY,STEPS)  )
   )$(
         SEXPORT(SECONDARY,"QUANTITY")   GT 0 AND
         SEXPORT(SECONDARY,"PRICE")      GT 0 AND
         SEXPORT(SECONDARY,"ELASTICITY") LT -0.05     )
$goto continue51

$label continue51
 + (SEXPORT(SECONDARY,"PRICE")*EXPORTS.L(SECONDARY))
            $(SEXPORT(SECONDARY,"PRICE")      GT 0 AND
              SEXPORT(SECONDARY,"ELASTICITY") GE -0.05
              AND SEXPORT(SECONDARY,"QUANTITY") NE 0    )

 - SECONDBAL.M(SECONDARY)* EXPORTS.L(SECONDARY)
;


IMPORPSS(SECONDARY)=



$if not "%solveasm%" == "nonlinear"      $goto linear61
$if     "%solveasm%" == "nonlinear"      $goto nlinear61

$label nlinear61
 - SIMPORT(SECONDARY,"ELASTICITY") /(1.+SIMPORT(SECONDARY,"ELASTICITY"))
  *(SIMPORT(SECONDARY,"PRICE")
  *(
  ((IMPORTS.L(SECONDARY)/SIMPORT(SECONDARY,"QUANTITY"))**
                  (1./SIMPORT(SECONDARY,"ELASTICITY"))
               * IMPORTS.L(SECONDARY)) ))
  $(SIMPORT(SECONDARY,"PRICE")      GT 0 AND
              SIMPORT(SECONDARY,"QUANTITY")   GT 0 AND
             SIMPORT(SECONDARY,"ELASTICITY") GT 0.05     )
$goto continue61

$label linear61
 - SIMPORT(SECONDARY,"ELASTICITY") /(1.+SIMPORT(SECONDARY,"ELASTICITY"))
  *(SIMPORT(SECONDARY,"PRICE")
  *(
      +SUM(STEPS,(QINC(STEPS))** (1./SIMPORT(SECONDARY,"ELASTICITY"))
               * QINC(STEPS)*SIMPORT(SECONDARY,"QUANTITY")
              *IMPORTSS.L(SECONDARY,STEPS)
  )  ))
  $(SIMPORT(SECONDARY,"PRICE")      GT 0 AND
              SIMPORT(SECONDARY,"QUANTITY")   GT 0 AND
             SIMPORT(SECONDARY,"ELASTICITY") GT 0.05     )

$goto continue61

$label continue61
   - (SIMPORT(SECONDARY,"PRICE")*IMPORTS.L(SECONDARY))
       $(SIMPORT(SECONDARY,"PRICE")      GT 0 AND
         SIMPORT(SECONDARY,"ELASTICITY") LE 0.05
         AND SIMPORT(SECONDARY,"QUANTITY") NE 0     )

  + IMPORTS.L(SECONDARY) * SECONDBAL.M(SECONDARY)

;

LANDPS(REGIONS,LANDTYPE)=

SUM(SUBREG$MAPPING(REGIONS,SUBREG),

$if not "%solveasm%" == "nonlinear"      $goto linear71
$if     "%solveasm%" == "nonlinear"      $goto nlinear71

$label nlinear71

- LANDSUPPL(LANDTYPE,SUBREG,"ELASTICITY")
  /(1.+LANDSUPPL(LANDTYPE,SUBREG,"ELASTICITY"))
  *(LANDSUPPL(LANDTYPE,SUBREG,"PRICE")
  *(
  ((LANDSUPPLY.L(SUBREG,LANDTYPE)
      /LANDSUPPL(LANDTYPE,SUBREG,"QUANTITY"))**
                  (1./LANDSUPPL(LANDTYPE,SUBREG,"ELASTICITY"))
               * LANDSUPPLY.L(SUBREG,LANDTYPE)) ))
 $(LANDSUPPL(LANDTYPE,SUBREG,"QUANTITY")   GT 0 AND
   LANDSUPPL(LANDTYPE,SUBREG,"PRICE")      GT 0 AND
   LANDSUPPL(LANDTYPE,SUBREG,"ELASTICITY") GT 0.05  )

$goto continue71

$label linear71
- LANDSUPPL(LANDTYPE,SUBREG,"ELASTICITY")
  /(1.+LANDSUPPL(LANDTYPE,SUBREG,"ELASTICITY"))
  *(LANDSUPPL(LANDTYPE,SUBREG,"PRICE")
  *(
      +SUM(STEPS,(QINC(STEPS))**
                 (1./LANDSUPPL(LANDTYPE,SUBREG,"ELASTICITY"))
               * QINC(STEPS)*LANDSUPPL(LANDTYPE,SUBREG,"QUANTITY")
              *LANDSUPPLS.L(SUBREG,LANDTYPE,STEPS) )   ))
 $(LANDSUPPL(LANDTYPE,SUBREG,"QUANTITY")   GT 0 AND
   LANDSUPPL(LANDTYPE,SUBREG,"PRICE")      GT 0 AND
   LANDSUPPL(LANDTYPE,SUBREG,"ELASTICITY") GT 0.05  )
$goto continue71

$label continue71
 - ( LANDSUPPL(LANDTYPE,SUBREG,"PRICE")*
     LANDSUPPLY.L(SUBREG,LANDTYPE))
         $(LANDSUPPL(LANDTYPE,SUBREG,"PRICE")      GT 0 AND
           LANDSUPPL(LANDTYPE,SUBREG,"ELASTICITY") LE 0.05 AND
           LANDSUPPL(LANDTYPE,SUBREG,"QUANTITY") NE 0.0  )

+ LAND.M(SUBREG,LANDTYPE)*LANDSUPPLY.L(SUBREG,LANDTYPE)
)
  ;


AUMPS(REGIONS)=

  SUM(SUBREG$MAPPING(REGIONS,SUBREG),

$if not "%solveasm%" == "nonlinear"      $goto linear81
$if     "%solveasm%" == "nonlinear"      $goto nlinear81

$label nlinear81
  - AUMSSUP(SUBREG,"PRIVATELAS")/(1.+AUMSSUP(SUBREG,"PRIVATELAS"))
    *(AUMSSUP(SUBREG,"PRIVATEP")
    *(
     ((AUMSPRIV.L(SUBREG)/AUMSSUP(SUBREG,"PRIVATEQ"))**
                  (1./AUMSSUP(SUBREG,"PRIVATELAS"))
    * AUMSPRIV.L(SUBREG)) ))
    $( AUMSSUP(SUBREG,"PRIVATEQ")      GT 0 AND
                AUMSSUP(SUBREG,"PRIVATEP")  GT 0 AND
                AUMSSUP(SUBREG,"PRIVATELAS")   GT 0.05   )
$goto continue81

$label linear81
  - AUMSSUP(SUBREG,"PRIVATELAS")/(1.+AUMSSUP(SUBREG,"PRIVATELAS"))
    *(AUMSSUP(SUBREG,"PRIVATEP")
    *(
    +SUM(STEPS,(QINC(STEPS))**(1./AUMSSUP(SUBREG,"PRIVATELAS"))
               * QINC(STEPS)*AUMSSUP(SUBREG,"PRIVATEQ")
              *AUMSPRIVS.L(SUBREG,STEPS)   )                  ))
    $( AUMSSUP(SUBREG,"PRIVATEQ")      GT 0 AND
                AUMSSUP(SUBREG,"PRIVATEP")  GT 0 AND
                AUMSSUP(SUBREG,"PRIVATELAS")   GT 0.05   )
$goto continue81

$label continue81
  - (AUMSSUP(SUBREG,"PRIVATEP")* AUMSPRIV.L(SUBREG))
          $(AUMSSUP(SUBREG,"PRIVATEP")  GT 0 AND
            AUMSSUP(SUBREG,"PRIVATELAS")   LE 0.05 AND
            AUMSSUP(SUBREG,"PRIVATEQ") NE 0.0     )

  - AUMSPUB.l(SUBREG)* AUMSSUP(SUBREG,"PUBLICPRC")

  + AUMSR.M(SUBREG)*(AUMSPRIV.L(SUBREG)+AUMSPUB.L(SUBREG))
  ) ;


WATERPS(REGIONS)=

  SUM(SUBREG$MAPPING(REGIONS,SUBREG),


$if not "%solveasm%" == "nonlinear"      $goto linear91
$if     "%solveasm%" == "nonlinear"      $goto nlinear91

$label nlinear91
  - WATERSUP(SUBREG,"PUMPELAS")   /(1.+WATERSUP(SUBREG,"PUMPELAS"))
    *(WATERSUP(SUBREG,"PUMPPRICE")
  *(
   ((WATERVAR.L(SUBREG)/WATERSUP(SUBREG,"PUMPQ"))**
                  (1./WATERSUP(SUBREG,"PUMPELAS"))
               * WATERVAR.L(SUBREG)) ))
  $( WATERSUP(SUBREG,"PUMPQ")      GT 0 AND
                WATERSUP(SUBREG,"PUMPPRICE")  GT 0 AND
                WATERSUP(SUBREG,"PUMPELAS")   GT 0.05   )
$goto continue91

$label linear91
  - WATERSUP(SUBREG,"PUMPELAS")   /(1.+WATERSUP(SUBREG,"PUMPELAS"))
    *(WATERSUP(SUBREG,"PUMPPRICE")
  *(
      +SUM(STEPS,(QINC(STEPS))**
                 (1./WATERSUP(SUBREG,"PUMPELAS"))
               * QINC(STEPS)*WATERSUP(SUBREG,"PUMPQ")
              *WATERVARS.L(SUBREG,STEPS)
     )
                  ))
  $( WATERSUP(SUBREG,"PUMPQ")      GT 0 AND
                WATERSUP(SUBREG,"PUMPPRICE")  GT 0 AND
                WATERSUP(SUBREG,"PUMPELAS")   GT 0.05   )
$goto continue91

$label continue91
  - (WATERSUP(SUBREG,"PUMPPRICE")* WATERVAR.L(SUBREG))
        $(WATERSUP(SUBREG,"PUMPPRICE")  GT 0 AND
          WATERSUP(SUBREG,"PUMPELAS")   LE 0.05 AND
          WATERSUP(SUBREG,"PUMPQ") NE 0.0     )

  - WATERFIX.l(SUBREG)* WATERSUP(SUBREG,"FIXEDPRC")

  + WATERR.M(SUBREG)*(WATERVAR.L(SUBREG)+WATERFIX.L(SUBREG))
  ) ;


LABORPS(REGIONS)=


$if not "%solveasm%" == "nonlinear"      $goto linear01
$if     "%solveasm%" == "nonlinear"      $goto nlinear01

$label nlinear01
- LABORSUP(REGIONS,"HIREELAS") /(1.+LABORSUP(REGIONS,"HIREELAS"))
                *(LABORSUP(REGIONS,"HIREP")
  *(
((HIRED.L(REGIONS)/LABORSUP(REGIONS,"HIREQ"))**
                  (1./LABORSUP(REGIONS,"HIREELAS"))
               * HIRED.L(REGIONS)) ))
 $(LABORSUP(REGIONS,"HIREQ")      GT 0 AND
               LABORSUP(REGIONS,"HIREP")      GT 0 AND
               LABORSUP(REGIONS,"HIREELAS")   GT 0.05     )
$goto continue01

$label linear01
- LABORSUP(REGIONS,"HIREELAS") /(1.+LABORSUP(REGIONS,"HIREELAS"))
                *(LABORSUP(REGIONS,"HIREP")
  *(
      +SUM(STEPS,(QINC(STEPS))**
                 (1./LABORSUP(REGIONS,"HIREELAS"))
               * QINC(STEPS)*LABORSUP(REGIONS,"HIREQ")
              *HIREDS.L(REGIONS,STEPS)    )         ))
 $(LABORSUP(REGIONS,"HIREQ")      GT 0 AND
               LABORSUP(REGIONS,"HIREP")      GT 0 AND
               LABORSUP(REGIONS,"HIREELAS")   GT 0.05     )
$goto continue01

$label continue01
  - (LABORSUP(REGIONS,"HIREP")* HIRED.l(REGIONS))
         $(LABORSUP(REGIONS,"HIREQ")    LE 0 AND
           LABORSUP(REGIONS,"HIREP")    GT 0 AND
           LABORSUP(REGIONS,"HIREELAS") LE 0.05 AND
           LABORSUP(REGIONS,"HIREQ") NE 0.0)

  - FAMILY.l(REGIONS)* LABORSUP(REGIONS,"FAMILYPRC")

  + HIRED.L(REGIONS)*LABOR.M(REGIONS)
;


PDCOST=
 -(SUM(SUBREG,
     SUM(CROP$FARMPROD("SLIPPAGE",CROP),
       SUM((WTECH,TECH),
           COSTC(SUBREG,CROP,WTECH,"PARTICIP",TECH)*
           CROPBUDGET.L(SUBREG,CROP,WTECH,"PARTICIP",TECH)
          +COSTC(SUBREG,CROP,WTECH,"NONPART",TECH)*
           CROPBUDGET.L(SUBREG,CROP,WTECH,"NONPART",TECH)))

    +SUM(CROP$(FARMPROD("SLIPPAGE",CROP) LE 0.0),
       SUM((WTECH,TECH),
           COSTC(SUBREG,CROP,WTECH,"BASE",TECH)*
           CROPBUDGET.L(SUBREG,CROP,WTECH,"BASE",TECH)))

    +SUM((ANIMAL,LIVETECH),
           COSTL(SUBREG,ANIMAL,LIVETECH)*
           LVSTBUDGET.L(SUBREG,ANIMAL,LIVETECH)))
    +SUM(INPUT$INPUTPRICE(INPUT),
         INPUTPRICE(INPUT)
         * SUM(PROCESSALT,
               PROCESS.L(PROCESSALT)*PROCBUD(INPUT,PROCESSALT)))
  )

 - SUM(PROCESSALT,PROCESS.L(PROCESSALT)*
                 (SUM(COST,PROCBUD(COST,PROCESSALT))))
;


FARPWEl=

+ SUM(CROP$(FARMPROD("TARGET",CROP) GT 0 AND
            FARMPROD("SLIPPAGE",CROP) GT 0 AND
            FARMPROD("MKTLOANY-N",CROP) GT 0),
    ( FARMPROD("DEFIC",CROP)
    + FARMPROD("MKTLOANY-N",CROP)*FARMPROD("MKTLOAN",CROP))
    * DEFPRODN.L(CROP)     )

+ SUM(CROP$(FARMPROD("TARGET",CROP) GT 0 AND
            FARMPROD("SLIPPAGE",CROP) GT 0 AND
            FARMPROD("MKTLOANY-N",CROP) LE 0 ),
    ( FARMPROD("DEFIC",CROP)*DEFPRODN.L(CROP)   )   )

+ SUM(CROP$(FARMPROD("TARGET",CROP)*FARMPROD("50-92",CROP)),
      FARMPROD("DEFIC",CROP)*PRDN5092.L(CROP) )

+ SUM(CROP$(FARMPROD("TARGET",CROP)*FARMPROD("0-92",CROP)),
      FARMPROD("DEFIC",CROP)*PRDN092.L(CROP) )

+ SUM(CROP$(FARMPROD("TARGET",CROP)*FARMPROD("DIVERSION",CROP)),
      FARMPROD("DIVERPAY",CROP)*DIVPRODN.L(CROP) )

+ SUM(CROP$(FARMPROD("TARGET",CROP)  GT 0 AND
            FARMPROD("FPYIELD",CROP) GT 1.0),
      FARMPROD("DEFIC",CROP)*ARTIF.L(CROP)  )

+ SUM(CROP$(FARMPROD("TARGET",CROP) GT 0 AND
            FARMPROD("UNHARVACR",CROP) GT 0),
      FARMPROD("DEFIC",CROP)*UNHARV.L(CROP)    )

+ SUM(PRIMARY$(FARMPROD("LOANRATE",PRIMARY) GT 0 AND
               FARMPROD("MKTLOANY-N",PRIMARY) LT 1.0),
      FARMPROD("LOANRATE",PRIMARY)*CCCLOANP.L(PRIMARY))

+ SUM(SECONDARY$(FARMPROD("LOANRATE",SECONDARY) GT 0 AND
                 FARMPROD("MKTLOANY-N",SECONDARY) LT 1.0),
      FARMPROD("LOANRATE",SECONDARY)*CCCLOANS.L(SECONDARY) )
;

 RESIDUAL=

 + SUM(PRIMARY,PRIMARYBAL.M(PRIMARY)*DEMANDP.L(PRIMARY))

 + SUM(PRIMARY,PRIMARYBAL.M(PRIMARY)* EXPORTP.L(PRIMARY))

 - SUM(PRIMARY,IMPORTP.L(PRIMARY)* PRIMARYBAL.M(PRIMARY))

 + SUM(SECONDARY,SECONDBAL.M(SECONDARY)*DEMANDS.L(SECONDARY))

 + SUM(SECONDARY,SECONDBAL.M(SECONDARY)* EXPORTS.L(SECONDARY))

 - SUM(SECONDARY,IMPORTS.L(SECONDARY) * SECONDBAL.M(SECONDARY))

 - SUM((SUBREG,LANDTYPE),
        LAND.M(SUBREG,LANDTYPE)* LANDSUPPLY.L(SUBREG,LANDTYPE))

 - SUM(SUBREG,
        AUMSR.M(SUBREG)*(AUMSPRIV.L(SUBREG)+AUMSPUB.L(SUBREG)))

 - SUM(SUBREG,
        WATERR.M(SUBREG)*(WATERVAR.L(SUBREG)+WATERFIX.L(SUBREG)))

 - SUM(REGIONS,HIRED.L(REGIONS)*LABOR.M(REGIONS))

;

DECOBJN=

  SUM(PRIMARY,
      DOMCSP(PRIMARY))

+ SUM(PRIMARY,
      EXPORCSP(PRIMARY)
    + IMPORPSP(PRIMARY))

+ SUM(SECONDARY,
      DOMCSS(SECONDARY)
    + EXPORCSS(SECONDARY)
    + IMPORPSS(SECONDARY))

+ SUM((REGIONS,LANDTYPE),
      LANDPS(REGIONS,LANDTYPE))

+ SUM(REGIONS,
      AUMPS(REGIONS)
    + WATERPS(REGIONS))

+ SUM(REGIONS,
      LABORPS(REGIONS))

+ PDCOST

+ FARPWEL

+ RESIDUAL

- SUM(PRIMARY,999999*ARTS.L(PRIMARY))

- SUM(SECONDARY,999999*ARTS.L(SECONDARY))

;

DOMCS= SUM(PRIMARY,DOMCSP(PRIMARY))
      +SUM(SECONDARY,DOMCSS(SECONDARY));
UEXPORTCS=SUM(PRIMARY,EXPORCSP(PRIMARY))+
          SUM(SECONDARY,EXPORCSS(SECONDARY));

UIMPORTPS=SUM(PRIMARY,IMPORPSP(PRIMARY))+
          SUM(SECONDARY,IMPORPSS(SECONDARY));

domps=
    SUM((REGIONS,LANDTYPE),LANDPS(REGIONS,LANDTYPE))
  + SUM(REGIONS,AUMPS(REGIONS) + WATERPS(REGIONS))
  + SUM(REGIONS,LABORPS(REGIONS))
  + PDCOST
  + FARPWEL
  + RESIDUAL
  -SUM(PRIMARY,999999*ARTS.L(PRIMARY))
  -SUM(SECONDARY,999999*ARTS.L(SECONDARY))
;
USTOTWEL=DOMCS+DOMPS+WELSUM("GOV-TOTPMT");

NEWOBJ=DOMPS+DOMCS+UEXPORTCS+UIMPORTPS;



OLDCSPS  =

( SUM(PRIMARY$(PDEMAND(PRIMARY,"QUANTITY")   GT 0 AND
               PDEMAND(PRIMARY,"PRICE")      GT 0 AND
               PDEMAND(PRIMARY,"ELASTICITY") LT -0.05     ),

$if not "%solveasm%" == "nonlinear"      $goto linear1
$if     "%solveasm%" == "nonlinear"      $goto nonlinear1

$label nonlinear1
   ((DEMANDP.L(PRIMARY)/PDEMAND(PRIMARY,"QUANTITY"))
                      **(1./PDEMAND(PRIMARY,"ELASTICITY"))
   * DEMANDP.L(PRIMARY) * PDEMAND(PRIMARY,"PRICE")
   * PDEMAND(PRIMARY,"ELASTICITY")  / (1.+PDEMAND(PRIMARY,"ELASTICITY"))
   + PDEMAND(PRIMARY,"CONSTANT1")+PDEMAND(PRIMARY,"CONSTANT2")))
$goto continue1

$label linear1
   + (SUM(STEPS$(PDEMAND(PRIMARY,"TFAC") GE 1/QINC(STEPS)),
            (QINC(STEPS) **(1./PDEMAND(PRIMARY,"ELASTICITY"))
   * PDEMAND(PRIMARY,"QUANTITY")*QINC(STEPS)   * PDEMAND(PRIMARY,"PRICE")
   * PDEMAND(PRIMARY,"ELASTICITY")   / (1.+PDEMAND(PRIMARY,"ELASTICITY"))
   +PDEMAND(PRIMARY,"CONSTANT1")   +PDEMAND(PRIMARY,"CONSTANT2")
    )*DEMANDPS.L(PRIMARY,STEPS))
    )    )
$goto continue1

$label continue1
+SUM(PRIMARY$(PDEMAND(PRIMARY,"PRICE")      GT 0 AND
               PDEMAND(PRIMARY,"ELASTICITY") GE -0.05
              AND PDEMAND(PRIMARY,"QUANTITY") NE 0   ),
               PDEMAND(PRIMARY,"PRICE")*DEMANDP.L(PRIMARY))

+SUM(PRIMARY$(PEXPORT(PRIMARY,"QUANTITY")   GT 0 AND
             PEXPORT(PRIMARY,"PRICE")      GT 0 AND
             PEXPORT(PRIMARY,"ELASTICITY") LT -0.05     ),

$if not "%solveasm%" == "nonlinear"      $goto linear2
$if     "%solveasm%" == "nonlinear"      $goto nonlinear2

$label nonlinear2
   ((EXPORTP.L(PRIMARY)/PEXPORT(PRIMARY,"QUANTITY"))
                      **(1./PEXPORT(PRIMARY,"ELASTICITY"))
   * EXPORTP.L(PRIMARY) * PEXPORT(PRIMARY,"PRICE")
   * PEXPORT(PRIMARY,"ELASTICITY") / (1.+PEXPORT(PRIMARY,"ELASTICITY"))
   +PEXPORT(PRIMARY,"CONSTANT1")+PEXPORT(PRIMARY,"CONSTANT2")  ))
$goto continue2


$label linear2
   + (SUM(STEPS$(PEXPORT(PRIMARY,"TFAC") GE 1/QINC(STEPS)),
                 (QINC(STEPS) **(1./PEXPORT(PRIMARY,"ELASTICITY"))
   * PEXPORT(PRIMARY,"QUANTITY")*QINC(STEPS) * PEXPORT(PRIMARY,"PRICE")
   * PEXPORT(PRIMARY,"ELASTICITY") / (1.+PEXPORT(PRIMARY,"ELASTICITY"))
   + PEXPORT(PRIMARY,"CONSTANT1") +PEXPORT(PRIMARY,"CONSTANT2"))
   * EXPORTPS.L(PRIMARY,STEPS)  )     ))
$goto continue2

$label continue2
+SUM(PRIMARY$(PEXPORT(PRIMARY,"PRICE")      GT 0 AND
               PEXPORT(PRIMARY,"ELASTICITY") GE -0.05
              AND PEXPORT(PRIMARY,"QUANTITY") NE 0    ),
               PEXPORT(PRIMARY,"PRICE")*EXPORTP.L(PRIMARY) )

-SUM(PRIMARY$(PIMPORT(PRIMARY,"PRICE")      GT 0 AND
              PIMPORT(PRIMARY,"QUANTITY")   GT 0 AND
             PIMPORT(PRIMARY,"ELASTICITY") GT 0.05     ),
   +PIMPORT(PRIMARY,"ELASTICITY") /(1.+PIMPORT(PRIMARY,"ELASTICITY"))
  *(PIMPORT(PRIMARY,"PRICE")
  *(

$if not "%solveasm%" == "nonlinear"      $goto linear3
$if     "%solveasm%" == "nonlinear"      $goto nonlinear3

$label nonlinear3
((IMPORTP.L(PRIMARY)/PIMPORT(PRIMARY,"QUANTITY"))**
                  (1./PIMPORT(PRIMARY,"ELASTICITY"))
               * IMPORTP.L(PRIMARY)) )))
$goto continue3

$label linear3
      +SUM(STEPS,(QINC(STEPS))** (1./PIMPORT(PRIMARY,"ELASTICITY"))
               * QINC(STEPS)*PIMPORT(PRIMARY,"QUANTITY")
              *IMPORTPS.L(PRIMARY,STEPS)
  )  )))
$goto continue3

$label continue3
-SUM(PRIMARY$(PIMPORT(PRIMARY,"PRICE")      GT 0 AND
               PIMPORT(PRIMARY,"ELASTICITY") LE 0.05
              AND PIMPORT(PRIMARY,"QUANTITY") NE 0      ),
               PIMPORT(PRIMARY,"PRICE")*IMPORTP.L(PRIMARY))

+SUM(SECONDARY$(SDEMAND(SECONDARY,"QUANTITY")   GT 0 AND
             SDEMAND(SECONDARY,"PRICE")      GT 0 AND
             SDEMAND(SECONDARY,"ELASTICITY") LT -0.05     ),

$if not "%solveasm%" == "nonlinear"      $goto linear4
$if     "%solveasm%" == "nonlinear"      $goto nonlinear4

$label nonlinear4
  ( (DEMANDS.L(SECONDARY)/SDEMAND(SECONDARY,"QUANTITY"))
                      **(1./SDEMAND(SECONDARY,"ELASTICITY"))
  * DEMANDS.L(SECONDARY)   * SDEMAND(SECONDARY,"PRICE")
  * SDEMAND(SECONDARY,"ELASTICITY")   / (1.+SDEMAND(SECONDARY,"ELASTICITY"))
  +SDEMAND(SECONDARY,"CONSTANT1")+SDEMAND(SECONDARY,"CONSTANT2")))
$goto continue4

$label linear4
   + (SUM(STEPS$(SDEMAND(SECONDARY,"TFAC") GE 1/QINC(STEPS)),
            (QINC(STEPS) **(1./SDEMAND(SECONDARY,"ELASTICITY"))
   * SDEMAND(SECONDARY,"QUANTITY")*QINC(STEPS)   * SDEMAND(SECONDARY,"PRICE")
   * SDEMAND(SECONDARY,"ELASTICITY")   / (1.+SDEMAND(SECONDARY,"ELASTICITY"))
   +SDEMAND(SECONDARY,"CONSTANT1")   +SDEMAND(SECONDARY,"CONSTANT2")
    )*DEMANDSS.L(SECONDARY,STEPS))
    )    )
$goto continue4

$label continue4
+SUM(SECONDARY$(SDEMAND(SECONDARY,"PRICE")      GT 0 AND
               SDEMAND(SECONDARY,"ELASTICITY") GE -0.05
              AND SDEMAND(SECONDARY,"QUANTITY") NE 0   ),
               SDEMAND(SECONDARY,"PRICE")*DEMANDS.L(SECONDARY))


+SUM(SECONDARY$(SEXPORT(SECONDARY,"QUANTITY")   GT 0 AND
                SEXPORT(SECONDARY,"PRICE")      GT 0 AND
                SEXPORT(SECONDARY,"ELASTICITY") LT -0.05     ),

$if not "%solveasm%" == "nonlinear"      $goto linear5
$if     "%solveasm%" == "nonlinear"      $goto nonlinear5

$label nonlinear5
   ((EXPORTS.L(SECONDARY)/SEXPORT(SECONDARY,"QUANTITY"))
                      **(1./SEXPORT(SECONDARY,"ELASTICITY"))
   * EXPORTS.L(SECONDARY)   * SEXPORT(SECONDARY,"PRICE")
   * SEXPORT(SECONDARY,"ELASTICITY") / (1.+SEXPORT(SECONDARY,"ELASTICITY"))
   +SEXPORT(SECONDARY,"CONSTANT1") +SEXPORT(SECONDARY,"CONSTANT2")  )
   )
$goto continue5

$label linear5
   + (SUM(STEPS$(SEXPORT(SECONDARY,"TFAC") GE 1/QINC(STEPS)),
                 (QINC(STEPS) **(1./SEXPORT(SECONDARY,"ELASTICITY"))
   * SEXPORT(SECONDARY,"QUANTITY")*QINC(STEPS)
   * SEXPORT(SECONDARY,"PRICE")   * SEXPORT(SECONDARY,"ELASTICITY")
   / (1.+SEXPORT(SECONDARY,"ELASTICITY"))
      +SEXPORT(SECONDARY,"CONSTANT1") +SEXPORT(SECONDARY,"CONSTANT2"))
   *EXPORTSS.L(SECONDARY,STEPS)  )
  ))
$goto continue5

$label continue5
+SUM(SECONDARY$(SEXPORT(SECONDARY,"PRICE")      GT 0 AND
               SEXPORT(SECONDARY,"ELASTICITY") GE -0.05
              AND SEXPORT(SECONDARY,"QUANTITY") NE 0    ),
               SEXPORT(SECONDARY,"PRICE")*EXPORTS.L(SECONDARY))

-SUM(SECONDARY$(SIMPORT(SECONDARY,"PRICE")      GT 0 AND
              SIMPORT(SECONDARY,"QUANTITY")   GT 0 AND
             SIMPORT(SECONDARY,"ELASTICITY") GT 0.05     ),
   +SIMPORT(SECONDARY,"ELASTICITY") /(1.+SIMPORT(SECONDARY,"ELASTICITY"))
  *(SIMPORT(SECONDARY,"PRICE")
  *(

$if not "%solveasm%" == "nonlinear"      $goto linear6
$if     "%solveasm%" == "nonlinear"      $goto nonlinear6

$label nonlinear6
((IMPORTS.L(SECONDARY)/SIMPORT(SECONDARY,"QUANTITY"))**
                  (1./SIMPORT(SECONDARY,"ELASTICITY"))
               * IMPORTS.L(SECONDARY)) )))
$goto continue6

$label linear6
      +SUM(STEPS,(QINC(STEPS))** (1./SIMPORT(SECONDARY,"ELASTICITY"))
               * QINC(STEPS)*SIMPORT(SECONDARY,"QUANTITY")
              *IMPORTSS.L(SECONDARY,STEPS)
  )  )))
$goto continue6

$label continue6
-SUM(SECONDARY$(SIMPORT(SECONDARY,"PRICE")      GT 0 AND
               SIMPORT(SECONDARY,"ELASTICITY") LE 0.05
              AND SIMPORT(SECONDARY,"QUANTITY") NE 0     ),
               SIMPORT(SECONDARY,"PRICE")*IMPORTS.L(SECONDARY))

-SUM((LANDTYPE,SUBREG)$(LANDSUPPL(LANDTYPE,SUBREG,"QUANTITY")   GT 0 AND
                          LANDSUPPL(LANDTYPE,SUBREG,"PRICE")      GT 0 AND
                          LANDSUPPL(LANDTYPE,SUBREG,"ELASTICITY") GT 0.05  ),
  +LANDSUPPL(LANDTYPE,SUBREG,"ELASTICITY")
  /(1.+LANDSUPPL(LANDTYPE,SUBREG,"ELASTICITY"))
  *(LANDSUPPL(LANDTYPE,SUBREG,"PRICE")
  *(

$if not "%solveasm%" == "nonlinear"      $goto linear7
$if     "%solveasm%" == "nonlinear"      $goto nonlinear7

$label nonlinear7
((LANDSUPPLY.L(SUBREG,LANDTYPE)
      /LANDSUPPL(LANDTYPE,SUBREG,"QUANTITY"))**
                  (1./LANDSUPPL(LANDTYPE,SUBREG,"ELASTICITY"))
               * LANDSUPPLY.L(SUBREG,LANDTYPE)) )))
$goto continue7

$label linear7
      +SUM(STEPS,(QINC(STEPS))**
                 (1./LANDSUPPL(LANDTYPE,SUBREG,"ELASTICITY"))
               * QINC(STEPS)*LANDSUPPL(LANDTYPE,SUBREG,"QUANTITY")
              *LANDSUPPLS.L(SUBREG,LANDTYPE,STEPS) )   )))
$goto continue7

$label continue7
-SUM((SUBREG,LANDTYPE)$(LANDSUPPL(LANDTYPE,SUBREG,"PRICE")      GT 0 AND
                          LANDSUPPL(LANDTYPE,SUBREG,"ELASTICITY") LE 0.05 AND
                          LANDSUPPL(LANDTYPE,SUBREG,"QUANTITY") NE 0.0  ),
                          LANDSUPPL(LANDTYPE,SUBREG,"PRICE")*
                          LANDSUPPLY.L(SUBREG,LANDTYPE))

-SUM(SUBREG$( AUMSSUP(SUBREG,"PRIVATEQ")      GT 0 AND
                AUMSSUP(SUBREG,"PRIVATEP")  GT 0 AND
                AUMSSUP(SUBREG,"PRIVATELAS")   GT 0.05   ),
    AUMSSUP(SUBREG,"PRIVATELAS")/(1.+AUMSSUP(SUBREG,"PRIVATELAS"))
    *(AUMSSUP(SUBREG,"PRIVATEP")
    *(

$if not "%solveasm%" == "nonlinear"      $goto linear8
$if     "%solveasm%" == "nonlinear"      $goto nonlinear8

$label nonlinear8
((AUMSPRIV.L(SUBREG)/AUMSSUP(SUBREG,"PRIVATEQ"))**
                  (1./AUMSSUP(SUBREG,"PRIVATELAS"))
    * AUMSPRIV.L(SUBREG)) )))
$goto continue8

$label linear8
    +SUM(STEPS,(QINC(STEPS))**(1./AUMSSUP(SUBREG,"PRIVATELAS"))
               * QINC(STEPS)*AUMSSUP(SUBREG,"PRIVATEQ")
              *AUMSPRIVS.L(SUBREG,STEPS)   )                  )))
$goto continue8

$label continue8
-SUM(SUBREG$(AUMSSUP(SUBREG,"PRIVATEP")  GT 0 AND
               AUMSSUP(SUBREG,"PRIVATELAS")   LE 0.05 AND
               AUMSSUP(SUBREG,"PRIVATEQ") NE 0.0     ),
               AUMSSUP(SUBREG,"PRIVATEP")* AUMSPRIV.L(SUBREG))

- SUM(SUBREG,AUMSPUB.L(SUBREG)* AUMSSUP(SUBREG,"PUBLICPRC"))

-SUM(SUBREG$( WATERSUP(SUBREG,"PUMPQ")      GT 0 AND
                WATERSUP(SUBREG,"PUMPPRICE")  GT 0 AND
                WATERSUP(SUBREG,"PUMPELAS")   GT 0.05   ),
    WATERSUP(SUBREG,"PUMPELAS")   /(1.+WATERSUP(SUBREG,"PUMPELAS"))
    *(WATERSUP(SUBREG,"PUMPPRICE")
  *(

$if not "%solveasm%" == "nonlinear"      $goto linear9
$if     "%solveasm%" == "nonlinear"      $goto nonlinear9

$label nonlinear9
((WATERVAR.L(SUBREG)/WATERSUP(SUBREG,"PUMPQ"))**
                  (1./WATERSUP(SUBREG,"PUMPELAS"))
               * WATERVAR.L(SUBREG)) )))
$goto continue9

$label linear9
      +SUM(STEPS,(QINC(STEPS))**
                 (1./WATERSUP(SUBREG,"PUMPELAS"))
               * QINC(STEPS)*WATERSUP(SUBREG,"PUMPQ")
              *WATERVARS.L(SUBREG,STEPS)
     )
                  )))
$goto continue9

$label continue9
-SUM(SUBREG$(WATERSUP(SUBREG,"PUMPPRICE")  GT 0 AND
               WATERSUP(SUBREG,"PUMPELAS")   LE 0.05 AND
               WATERSUP(SUBREG,"PUMPQ") NE 0.0     ),
               WATERSUP(SUBREG,"PUMPPRICE")*
               WATERVAR.L(SUBREG))

-SUM(REGION$(LABORSUP(REGION,"HIREQ")      GT 0 AND
               LABORSUP(REGION,"HIREP")      GT 0 AND
               LABORSUP(REGION,"HIREELAS")   GT 0.05     ),
          LABORSUP(REGION,"HIREELAS") /(1.+LABORSUP(REGION,"HIREELAS"))
                *(LABORSUP(REGION,"HIREP")
  *(

$if not "%solveasm%" == "nonlinear"      $goto linear0
$if     "%solveasm%" == "nonlinear"      $goto nonlinear0

$label nonlinear0
((HIRED.L(REGION)/LABORSUP(REGION,"HIREQ"))**
                  (1./LABORSUP(REGION,"HIREELAS"))
               * HIRED.L(REGION)) )))
$goto continue0

$label linear0
      +SUM(STEPS,(QINC(STEPS))**
                 (1./LABORSUP(REGION,"HIREELAS"))
               * QINC(STEPS)*LABORSUP(REGION,"HIREQ")
              *HIREDS.L(REGION,STEPS)    )         )))
$goto continue0

$label continue0
- SUM(REGION$(LABORSUP(REGION,"HIREQ")    LE 0 AND
                     LABORSUP(REGION,"HIREP")    GT 0 AND
                     LABORSUP(REGION,"HIREELAS") LE 0.05 AND
                     LABORSUP(REGION,"HIREQ") NE 0.0),
                     LABORSUP(REGION,"HIREP")*
                     HIRED.L(REGION))


- SUM(REGION,FAMILY.L(REGION)* LABORSUP(REGION,"FAMILYPRC"))

-(SUM(SUBREG,
     SUM(CROP$FARMPROD("SLIPPAGE",CROP),
        sum((WTECH,TECH),
           costc(SUBREG,CROP,WTECH,"particip",TECH)*
           CROPBUDGET.L(SUBREG,CROP,WTECH,"PARTICIP",TECH)
          +costc(SUBREG,CROP,WTECH,"nonpart",TECH)*
           CROPBUDGET.L(SUBREG,CROP,WTECH,"nonpart",TECH)))
    +SUM(CROP$(FARMPROD("SLIPPAGE",CROP) LE 0.0),
        sum((WTECH,TECH),
           costc(SUBREG,CROP,WTECH,"base",TECH)*
           CROPBUDGET.L(SUBREG,CROP,WTECH,"base",TECH)))
    +SUM((ANIMAL,LIVETECH),
        costl(SUBREG,ANIMAL,LIVETECH)*
        LVSTBUDGET.L(SUBREG,ANIMAL,LIVETECH)))+
     SUM(INPUT$INPUTPRICE(INPUT),INPUTPRICE(INPUT)*
        SUM(PROCESSALT,PROCESS.L(PROCESSALT)*PROCBUD(INPUT,PROCESSALT))))

- SUM(PROCESSALT,PROCESS.L(PROCESSALT)*
           (SUM(COST,PROCBUD(COST,PROCESSALT))))

- SUM(SUBREG,WATERFIX.L(SUBREG)* WATERSUP(SUBREG,"FIXEDPRC"))

+ SUM(CROP$(FARMPROD("TARGET",CROP) GT 0 AND
            FARMPROD("SLIPPAGE",CROP) GT 0 AND
            FARMPROD("MKTLOANY-N",CROP) GT 0),
  (FARMPROD("DEFIC",CROP)+FARMPROD("MKTLOANY-N",CROP)*FARMPROD("MKTLOAN",CROP))
    *DEFPRODN.L(CROP)     )

+ SUM(CROP$(FARMPROD("TARGET",CROP) GT 0 AND
            FARMPROD("SLIPPAGE",CROP) GT 0 AND
            FARMPROD("MKTLOANY-N",CROP) LE 0),
  (FARMPROD("DEFIC",CROP)*DEFPRODN.L(CROP)   )   )

+ SUM(CROP$(FARMPROD("TARGET",CROP)*FARMPROD("50-92",CROP)),
        FARMPROD("DEFIC",CROP)*PRDN5092.L(CROP) )

+ SUM(CROP$(FARMPROD("TARGET",CROP)*FARMPROD("0-92",CROP)),
        FARMPROD("DEFIC",CROP)*PRDN092.L(CROP) )

+ SUM(CROP$(FARMPROD("TARGET",CROP)*FARMPROD("diversion",CROP)),
        FARMPROD("DIVERPAY",CROP)*DIVPRODN.L(CROP)  )

+ SUM(CROP$(FARMPROD("TARGET",CROP)  GT 0 AND
            FARMPROD("FPYIELD",CROP) GT 1.0),
        FARMPROD("DEFIC",CROP)*ARTIF.L(CROP)    )

+ SUM(CROP$(FARMPROD("TARGET",CROP) GT 0 AND
              FARMPROD("UNHARVACR",CROP) GT 0),
        FARMPROD("DEFIC",CROP)*UNHARV.L(CROP)    )

+ SUM(PRIMARY$(FARMPROD("LOANRATE",PRIMARY) GT 0 AND
               FARMPROD("MKTLOANY-N",PRIMARY) LT 1.0),
       FARMPROD("LOANRATE",PRIMARY)*CCCLOANP.L(PRIMARY) )

+ SUM(SECONDARY$(FARMPROD("LOANRATE",SECONDARY) GT 0 AND
               FARMPROD("MKTLOANY-N",SECONDARY) LT 1.0),
       FARMPROD("LOANRATE",SECONDARY)*CCCLOANS.L(SECONDARY) ))

-SUM(prIMARY,999999*ARTS.L(PRIMARY))
-SUM(secondARY,999999*ARTS.L(secondARY))

;


display balancep;
DISPLAY DOMCSP
          EXPORCSP
          IMPORPSP
          DOMCSS
          EXPORCSS
          IMPORPSS
          LANDPS
          AUMPS
          WATERPS
          LABORPS
          ;
DISPLAY PDCOST
       FARPWEL
       RESIDUAL ;

DISPLAY DECOBJN
       CSPS.L
       DOMCS
       DOMPS
       UIMPORTPS
       UEXPORTCS
       USTOTWEL
       NEWOBJ
       OLDCSPS  ;
$ontext
#user model library stuff
Main topic ASM Model
Featured item 1 Report writing
Featured item 2 .L attribute
Featured item 3 .M attribute
Featured item 4 ASM sector model
Description
Defines report writing setup for ASM model
$offtext
